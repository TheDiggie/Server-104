// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Spirit is Human

constants:

   include blakston.khd

   EQUIPMENT_DROP_PERCENT = 100

resources:

   Spirit_name_rsc = "lost spirit"
   Spirit_desc_rsc = \
   "This is a poor lost soul. "
   Spirit_dead_name_rsc = "dead spirit"

classvars:

   vrName = Spirit_name_rsc
   vrDesc = Spirit_desc_rsc
   vrDead_name = Spirit_dead_name_rsc

   viTreasure_type = TID_EMPTY

   viSpeed = SPEED_FASTER
   viAttack_type = ATCK_WEAP_SLASH
   viAttributes = 0
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_WIZARD_KILLER

   viVisionDistance = 12

   viCashMin = 2000
   viCashMax = 6000

   viDead_drawfx = DRAWFX_TRANSLUCENT_50

properties:

   piBaseLevel = 120
   piBaseDifficulty = 6

   //Here so they can be set
   vrIcon = human_male_icon_rsc
   vrDead_Icon = human_dead_male_icona_rsc
   vrDead_Legs = human_dead_male_iconb_rsc
   viLevel = 120         // piBaseLevel
   viDifficulty = 4     // piBaseDifficulty
   piGender = GENDER_MALE
   viKarma = 0
   
   piDrawfx = DRAWFX_TRANSLUCENT_50

messages:

   SetEquipment()
   {
      local oEquipmentItem, iRandomNumber, iBonusModifier;

      iRandomNumber = Random(1,100);
      if iRandomNumber <= 35
      {
         oEquipmentItem = Create(&Longsword);
         iBonusModifier = 3;
      }
      else if iRandomNumber <= 55
      {
         oEquipmentItem = Create(&Axe);
         iBonusModifier = 2;
      }
      else if iRandomNumber <= 65
      {
         oEquipmentItem = Create(&Hammer);
         iBonusModifier = 2;
      }
      else if iRandomNumber <= 75
      {
         oEquipmentItem = Create(&Mace);
         iBonusModifier = 1;
      }
      else if iRandomNumber <= 80
      {
         oEquipmentItem = Create(&ShortSword);
         iBonusModifier = 1;
      }
      else if iRandomNumber <= 85
      {
         oEquipmentItem = Create(&Scimitar);
         iBonusModifier = 4;
      }
      else if iRandomNumber <= 90
      {
        oEquipmentItem = Create(&BattleBow);
        poArrow = Create(&NeruditeArrow);
        iBonusModifier = 5;
      }
      else
      {
         oEquipmentItem = Create(&LongBow);
         poArrow = Create(&NeruditeArrow);
         iBonusModifier = 4;
      }

      viDifficulty = piBaseDifficulty + iBonusModifier;
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      iRandomNumber = Random(1,100);
      if iRandomNumber <= 35
      {
         oEquipmentItem = Create(&LeatherArmor);
         iBonusModifier = 20;
      }
      else if iRandomNumber <= 70
      {
         oEquipmentItem = Create(&ChainArmor);
         iBonusModifier = 50;
      }
      else if iRandomNumber <= 85
      {
         oEquipmentItem = Create(&ScaleArmor);
         iBonusModifier = 75;
      }
      else
      {
         oEquipmentItem = Create(&PlateArmor);
         iBonusModifier = 100;
      }

      viLevel = piBaseLevel + iBonusModifier;
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      // Gauntlets?
      iRandomNumber = Random(1,100);
      if iRandomNumber < 20
      {
         oEquipmentItem = Create(&Gauntlet);
         viDifficulty = viDifficulty + 1;
         viLevel = viLevel + 20;
         Send(self,@AddEquipmentObject,#what=oEquipmentItem);
      }

      // Helmet?
      if Random(1,100) < 30
      {
         oEquipmentItem = Create(&SimpleHelm);
         Send(self,@AddEquipmentObject,#what=oEquipmentItem);
      }

      
         if Random(1,10) <= 8
         {
              iRandomNumber = Random(1,100);

           if iRandomNumber <= 40
           {
               oEquipmentItem = Create(&MetalShield);
           }
           else if iRandomNumber <= 80
           {
               oEquipmentItem = Create(&GoldShield);
           }
           else if iRandomNumber <= 98
           {
               oEquipmentItem = Create(&NeruditeShield);
           }
           else
           {
              oEquipmentItem = Create(&KnightShield);
           }
             Send(self,@AddEquipmentObject,#what=oEquipmentItem);
         }

      return;
   }


   // Override from monster superclass
   // We drop some of our carried items when we die.
   CreateTreasure(who = $, corpse = $)
   {
      local oUsedItem, oItemAtt, iPercentDrop;
	  
	  iPercentDrop = EQUIPMENT_DROP_PERCENT;
	  
      oItemAtt = Send(SYS,@FindItemAttByNum,#num=IA_CORPSEPOINTER);

      foreach oUsedItem in plUsing
      {
         // Weapon Wands should only have a 1% chance to drop.
		 if IsClass(oUsedItem, &ApprenticeWand)
		 {
		    iPercentDrop = 1;
		 }
		 else
		 {
		    iPercentDrop = EQUIPMENT_DROP_PERCENT;
		 }
		 
         // Only a percentage chance to drop each item.  Too much stuff otherwise.
         // Don't drop the shield!  It's a quest/special item!
		 if (Random(1,100) <= iPercentDrop)
            AND (NOT IsClass(oUsedItem,vcShieldClass))
            AND NOT pbIllusion
         {
            if (oItemAtt <> $) AND Send(oItemAtt,@ReqAddToItem,#oItem=oUsedItem)
            {
               Send(oItemAtt,@AddToItem,#oItem=oUsedItem,#state1=corpse);
            }

            Send(poOwner,@NewHold,#what=oUsedItem,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oUsedItem,@Delete);
         }
      }

      plUsing = $;

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
