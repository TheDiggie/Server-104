// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Ogre is Monster

constants:

   include blakston.khd

resources:
   
   ogre_name_rsc = "ogre"
   ogre_icon_rsc = ogre.bgf
   ogre_desc_rsc = \
      "This huge creature drives fear into the heart of anyone  "
      "who comes across it. Ogres are known to be much more  "
      "violent then friendly.  "


   ogre_dead_icon_rsc = ogreX.bgf
   ogre_dead_name_rsc = "dead ogre"

   ogre_sound_hit = ogre_att.mp3
   ogre_sound_miss = ogre_att.mp3
   ogre_sound_death = ogre_death.mp3
   ogre_sound_aware = ogre_miss.mp3

classvars:

   vrName = ogre_name_rsc
   vrIcon = ogre_icon_rsc
   vrDesc = ogre_desc_rsc
   vrDead_icon = ogre_dead_icon_rsc
   vrDead_name = ogre_dead_name_rsc

   viTreasure_type = TID_OGRE
   viSpeed = SPEED_FAST
   viAttributes = 0
   viLevel = 200
   viDifficulty = 8
   viVisionDistance = 18
   // Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viKarma = -100
   viDefault_behavior = AI_FIGHT_HYPERAGGRESSIVE | AI_FIGHT_SWITCHALOT
   viCashmin = 2000
   viCashmax = 6000
   vrSound_hit = ogre_sound_hit
   vrSound_miss = ogre_sound_miss
   vrSound_aware = ogre_sound_aware
   vrSound_death = ogre_sound_death

   viBonusXP = 0
   viBonusTP = 0

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_ACID, 80 ],
                        [ATCK_WEAP_NERUDITE, -10 ],
                        [ATCK_WEAP_NONMAGIC, 80 ] ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_ONCE);
      AddPacket(4,400,2,4,2,7,2,1);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,6,2,11);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,2,2,5,2,1);

         return;
      }

      // if no body animation
      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
