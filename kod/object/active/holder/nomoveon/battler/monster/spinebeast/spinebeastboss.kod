// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
SpinebeastBoss is Spinebeast

constants:

   include blakston.khd

resources:

   include spinebeastboss.lkod

   SpinebeastBoss_koc_name_rsc = "gikokimakao"
   SpinebeastBoss_name_rsc = "spinebeast behemoth"
   SpinebeastBoss_icon_rsc = spinebeastbehemoth.bgf
   SpinebeastBoss_desc_rsc = \
    "The fabled spinebeast behemoth is a rare and terrifying sight indeed.  "
    "Hulking at a height of over fifteen feet, few have seen the beast up close "
    "and even fewer have lived to tell the tale.  A heavily armored beast of spikes "
    "and plating, the spinebeast behemoth is the natural and uncontested ruler over "
    "its territory and more than ready to tear apart anyone who would dare to "
    "challenge this fact.  Obvious to anyone, it would be utter suicide to try and "
    "take on such an enormous foe on one's own."

   SpinebeastBoss_spawn = \
      "The behemoth spinebeast rears up on its hind legs, and a child spinebeast "
      "suddenly pops into existence!"

   SpinebeastBoss_dead_icon_rsc = spinebeastbehemothX.bgf
   SpinebeastBoss_dead_name_rsc = "dead spinebeast behemoth"

   SpinebeastBoss_sound_hit = yti_atkh.ogg
   SpinebeastBoss_sound_miss = yti_atkm.ogg
   SpinebeastBoss_sound_death = yti_dth.ogg
   SpinebeastBoss_sound_aware = yti_awr.ogg

classvars:

   vrKocName = SpinebeastBoss_koc_name_rsc
   vrName = SpinebeastBoss_name_rsc
   vrIcon = SpinebeastBoss_icon_rsc
   vrDesc = SpinebeastBoss_desc_rsc
   vrDead_icon = SpinebeastBoss_dead_icon_rsc
   vrDead_name = SpinebeastBoss_dead_name_rsc

   viTreasure_type = TID_WIMPY_MEDIUM
   viSpeed = SPEED_FAST
   viAttack_type = ATCK_WEAP_THRUST
   viDefault_behavior = AI_FIGHT_AGGRESSIVE  | AI_FIGHT_HYPERAGGRESSIVE
   viLevel = 250
   viDifficulty = 9
   viVisionDistance = 30
   viMeleeRange = 192
   viKarma = 0   
   viCashmin = 5000
   viCashmax = 10000
   vrSound_hit = SpinebeastBoss_sound_hit
   vrSound_miss = SpinebeastBoss_sound_miss
   vrSound_aware = SpinebeastBoss_sound_aware
   vrSound_death = SpinebeastBoss_sound_death

   vrSpawnChild = SpinebeastBoss_spawn

   viBleedChance = 50
   viBleedStrength = 5000
   viBleedDuration = 10000

   viFatigueChance = 20
   viFatigueStrength = 2000
   viFatigueDuration = 10000

   viBonusHealth = 200
   viBonusVigor = 500
   viBonusArmor = 4

   viBonusMight = 50
   viBonusStamina = 50

   vbCanSiphon = FALSE
   vbCanSeduce = FALSE

   viMaxChildSpawns = 20

   // Too boss to be pushed around.
   vbCan_be_pushed = FALSE

properties:

   piAnimation = ANIM_NONE

   ptSpawnSpinebeast = $
   piSpawnChildTime = 10000

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_COLD, 90],
                        [-ATCK_SPELL_SHOCK, 90],
                        [-ATCK_SPELL_ACID, 10 ],
                        [-ATCK_SPELL_FIRE, 10 ],
                        [ATCK_WEAP_SLASH, 55 ],
                        [ATCK_WEAP_NERUDITE, 25 ],
                        [ATCK_WEAP_PIERCE, 70 ],
                        [ATCK_WEAP_THRUST, 70 ],
                        [ATCK_WEAP_BLUDGEON, 30 ] ];

      plReputationEnemies = [REP_UNDEAD];

      ptSpawnSpinebeast = CreateTimer(self,@SpawnSpinebeast,piSpawnChildTime);

      propagate;
   }

   Delete()
   {
      if ptSpawnSpinebeast <> $
      {
         DeleteTimer(ptSpawnSpinebeast);
      }
      ptSpawnSpinebeast = $;

      propagate;
   }

   NewOwner(what=$)
   {
      if (what <> $
         AND ptSpawnSpinebeast = $)
      {
         ptSpawnSpinebeast = CreateTimer(self,@SpawnSpinebeast,piSpawnChildTime);
      }

      propagate;
   }

   SpawnSpinebeast()
   {
      local oBunny, oObject, i, bValid_location, iTries, iRow, iCol,
            iRow_sign, iCol_sign;

      ptSpawnSpinebeast = $;

      // Don't start up a new timer if $ owner.
      if (poOwner = $)
      {
         return;
      }

      if Send(poOwner,@IsUserInRoom)
         AND Send(poOwner,@CountHoldingHowMany,#class=&Spinebeast)
            < viMaxChildSpawns
      {
         iTries = 0;
         bValid_location = FALSE;

         // Try 7 times
         while NOT bValid_location
            AND iTries < 7
         {
            iTries = iTries + 1;
            if Random(1,2) = 1
            {
               iRow_sign= -1;
            }
            else
            {
               iRow_sign= 1;
            }

            if Random(1,2) = 1
            {
               iCol_sign= -1;
            }
            else
            {
               iCol_sign= 1;
            }

            iRow = piRow + iRow_sign*Random(1,3);
            iCol = piCol + iCol_sign*Random(1,3);

            if iRow >= 1
               AND iRow <= Send(poOwner,@GetRoomRows)
               AND iCol >= 1
               AND iCol <= Send(poOwner,@GetRoomCols)
            {
               bValid_location = TRUE;
            }
         }

         if bValid_location
         {
            oBunny = Create(&Spinebeast);
            Send(poOwner,@NewHold,#what=oBunny,#new_row=iRow,#new_col=iCol);
            if poTarget <> $
               AND IsClass(poTarget,&Player)
            {
               Post(oBunny,@TargetSwitch,#what=poTarget,#iHatred=100);
               Post(oBunny,@EnterStateEngage,#target=poTarget,#actnow=True);
            }

            // Do an attack animation.
            Send(self,@DoMeleeAnimation);

            foreach i in Send(poOwner,@GetHolderActive)
            {
               oObject = Send(poOwner,@HolderExtractObject,#data=i);
               if IsClass(oObject,&Player)
               {
                  //Send(oObject,@MsgSendUser,#message_rsc=vrSpawnChild);
               }
            }
         }
      }

      ptSpawnSpinebeast = CreateTimer(self,@SpawnSpinebeast,piSpawnChildTime);

      return;
   }

   DestroyDisposable()
   {
      return;
   }

   CanMorphTo()
   {
      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
