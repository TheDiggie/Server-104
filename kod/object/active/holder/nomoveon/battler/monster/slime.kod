// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Slime is Monster

constants:

   include blakston.khd

resources:

   include slime.lkod

   slime_koc_name_rsc = "kinachot"
   slime_name_rsc = "slime"
   slime_icon_rsc = amoeba.bgf
   slime_desc_rsc = \
   "A mass of quivering goo, the slime inches forward "
   "leaving a sickly trail behind.  The remains of its last "
   "victim are visible within its transparent innards."

   slime_dead_icon_rsc = amoebaX.bgf
   slime_dead_name_rsc = "dead slime"

   slime_sound_hit = slm_atkh.ogg
   slime_sound_miss = slm_atkm.ogg
   slime_sound_death = slm_dth.ogg
   slime_sound_aware = slm_awr.ogg

classvars:

   vrKocName = slime_koc_name_rsc
   vrName = slime_name_rsc
   vrIcon = slime_icon_rsc
   vrDesc = slime_desc_rsc
   vrDead_icon = slime_dead_icon_rsc
   vrDead_name = slime_dead_name_rsc

   viTreasure_type = TID_WIMPY_MEDIUM
   viSpeed = SPEED_SLOW
   viAttack_spell = ATCK_SPELL_ACID
   viAttributes = 0
   viLevel = 35
   viDifficulty = 4
   viVisionDistance = 10
   viKarma = 0
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_SINGLEMINDED
   viCashmin = 125
   viCashmax = 300
   vrSound_hit = slime_sound_hit
   vrSound_miss = slime_sound_miss
   vrSound_aware = slime_sound_aware
   vrSound_death = slime_sound_death

properties:

   piAnimation = ANIM_NONE

   piColor_translation = PT_GRAY_TO_LGREEN

   //transparency disabled, the color translation doesnt apply to the description when it is on
   //piDrawfx = DRAWFX_TRANSLUCENT_75

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_SHOCK, 45 ],
                        [-ATCK_SPELL_UNHOLY, 45 ],
                        [-ATCK_SPELL_HOLY, 45 ],
                        [-ATCK_SPELL_ACID, 45 ],
                        [ATCK_WEAP_ALL, 45 ],
                        [ATCK_WEAP_BLUDGEON, 10 ],
                        [-ATCK_SPELL_COLD, -25 ] ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      if piColor_translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_translation);
      }
	  
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,400,2,2,2,5);

      return;
   }

   SendMoveAnimation()
   {
      if piColor_translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_translation);
      }
	  
      AddPacket(1,ANIMATE_CYCLE,4,180,2,4,2,5);

      return;
   }

   SendAnimation()
   {
      if piColor_translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_translation);
      }
	  
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,2,2,4,2,1);

         return;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
