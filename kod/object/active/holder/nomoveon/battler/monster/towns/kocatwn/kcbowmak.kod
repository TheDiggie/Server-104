// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
KocatanBowMaker is KocatanTown

constants:

   include blakston.khd

resources:

   include kcbowmak.lkod

   KocatanBowMaker_name_rsc = "Hanla zax'Ta"
   KocatanBowMaker_icon_rsc = kcbowmak.bgf
   KocatanBowMaker_desc_rsc = \
      "This becoming, young woman appears as cold and curt to new faces.  "
      "Despite that, it is obvious to all that her true passion lies in the "
      "crafting of fine bows."

   KocatanBowMaker_KriipaClaw_sale = \
      "I have claws from several Kriipa I've killed in the jungle.  The "
      "Kriipa is a truly fearsome beast, as I'm sure you know, and a hunter "
      "who wishes to obtain their claws must pay for them in blood spilt in "
      "battle.  You will understand, then, that my price for them is high."
   KocatanBowMaker_unwanted_give = \
      "~k%s%s tells you, \"I have enough of those type of supplies to hold "
      "us through the summer.~n~k\""  
   KocatanBowMaker_already_have_spell = \
      "You already have knowledge of the %s spell.  You waste my time."
   KocatanBowMaker_not_ready = \
      "Your not ready to learn how to craft that.  "

classvars:

   vrName = KocatanBowMaker_name_rsc
   vrIcon = KocatanBowMaker_icon_rsc
   vrDesc = KocatanBowMaker_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM \
      | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE \
      | MOB_TEACHER
   viOccupation = MOB_ROLE_BLACKSMITH

   viGender = GENDER_FEMALE

   vrUnwanted_give = KocatanBowMaker_unwanted_give

properties:

messages:

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-2);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         if IsClass(what,&LongBow)
         {
            Send(self,@SetMood,#new_mood=piMood-2);
         }

         return;
      }

      return;
   }

   UserEntered(who=$)
   {
      if Send(who,@GetGender) = GENDER_MALE
      {
         Send(self,@SetMood,#new_mood=piMood-1);
      }

      if Send(who,@GetGender) = GENDER_FEMALE
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [ 
         [ 
            Create(&Arrow),
            Create(&LongBow),
            Create(&DrawKnife)
         ],
         $,
         [
            SID_CRAFTBATTLEBOW,
            SID_CRAFTLONGBOW
         ],
         $];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj=Create(&KriipaClaw,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,250,
            KocatanBowMaker_KriipaClaw_sale]);

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if sid = SID_CRAFTBATTLEBOW
         OR sid = SID_CRAFTLONGBOW
      {
         if NOT Send(self,@SpecialTeachChecks,#report=TRUE,#who=who,#num=sid)
         {
            return FALSE;
         }
      }

      propagate;
   }

   CanAddSpell(who=$,num=0,report=FALSE)
   {
      if NOT Send(self,@SpecialTeachChecks,#report=report,#who=who,#num=num)
      {
         return FALSE;
      }

      propagate;
   }

   SpecialTeachChecks(who=$,num=0,report=FALSE)
   {
      local i, oSkill, oSpell, iSchoolRequired, bFound;

      if who = $ OR num = 0
      {
         Debug("Called SpecialTeachChecks with bad parameters!");

         return FALSE;
      }

      oSpell = Send(SYS,@FindSpellByNum,#num=num);

      if NOT Send(oSpell,@IsAccessible)
      {
         return FALSE;
      }

      if Send(who,@HasSpell,#num=num)
      {
         if report
         {
            Post(self,@SayToOne,#target=who,
                  #message_rsc=KocatanBowMaker_already_have_spell,
                  #parm1=Send(oSpell,@GetName));
         }

         return FALSE;
      }

      bFound = FALSE;
      iSchoolRequired=0;

      if num = SID_CRAFTBATTLEBOW
      {
         iSchoolRequired = SKS_FENCING;
      }

      if num = SID_CRAFTLONGBOW
      {
         iSchoolRequired = SKS_FENCING;
      }

      if iSchoolRequired = 0
      {
         return TRUE;
      }

      // Special checks for high level spells: need a high level spell in 
      // a particular school.
      foreach i in Send(who,@GetSkillList)
      {
         oSkill = Send(SYS,@FindSkillByNum,
                        #num=Send(who,@DecodeSkillNum,#compound=i));
         if Send(oSkill,@GetLevel) > 4
            AND Send(oSkill,@GetSchool) = iSchoolRequired
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound = FALSE
      {
         if report
         {
            Post(self,@SayToOne,#target=who,#message_rsc=KocatanBowMaker_not_ready);
         }

         return FALSE;
      }

      return TRUE;
   }
   
end
////////////////////////////////////////////////////////////////////////////////
