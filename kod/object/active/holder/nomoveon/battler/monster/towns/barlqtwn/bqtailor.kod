// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Barloquetailor is BarloqueTown

constants:

   include blakston.khd

resources:

   include bqtailor.lkod

   Barloquetailor_name_rsc = "Madelia"
   Barloquetailor_icon_rsc = bqtailor.bgf
   Barloquetailor_desc_rsc = \
      "Used to working with the well refined tastes of nobility, Madelia "
      "meticulously oversees her craft. However, she often wishes to spend "
      "excessive time working on a single outfit for one of the more powerful "
      "and esteemed members of court. Madelia's other hobby is to play with "
      "the hearts of men and currently has several affairs in the works."

   Batail_entry_welcome = \
      "Hail %s%s.  What are those dreadful things thou doth wear?"
   Batail_unwanted_give = \
      "~k%s%s tells you, \"Thou should seek a proper merchant to sell "
      "thy wares.~n~k\""

   Batail_already_have_spell = \
      "You already have knowledge of the %s spell.  You waste my time."
   Batail_not_ready = \
      "Your not ready to learn how to craft magic robes.  "

classvars:

   vrName = Barloquetailor_name_rsc
   vrIcon = Barloquetailor_icon_rsc
   vrDesc = Barloquetailor_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE
   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_NOMOVE | MOB_LISTEN \
      | MOB_RECEIVE | MOB_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_TAILOR
   viGender = GENDER_FEMALE

   vrUnwanted_give = Batail_unwanted_give

properties:

messages:

   AffectMood(why=$,what=$)
   {
      if (why = MOODMOD_FACTION_CHANGE)
      {
         if (what = FACTION_DUKE)
         {
            Send(self,@SetMood,#new_mood=piMood-3);

            return;
         }

         if (what = FACTION_PRINCESS)
         {
            Send(self,@SetMood,#new_mood=piMood+3);

            return;
         }
      }

      if (why = MOODMOD_DAWN)
      {
         Send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      if (why = MOODMOD_DUSK)
      {
         Send(self,@SetMood,#new_mood=piMood-2);

         return;
      }

      if (why = MOODMOD_SELL_ITEM)
      {
         Send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      return;
   }

   UserEntered(who=$)
   {
      Post(who,@SomeoneSaid,#what=self,#string=Batail_entry_welcome,
            #type=SAY_RESOURCE,
            #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      if (Send(who,@GetHomeRoom) = RID_BAR_INN)
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      if (Send(who,@GetHomeRoom) = RID_TOS_INN)
      {
         Send(self,@SetMood,#new_mood=piMood-2);
      }

      if (Send(who,@GetHomeRoom) = RID_JAS_INN)
      {
         Send(self,@SetMood,#new_mood=piMood-1);
      }

      if (Send(who,@GetGender) = GENDER_MALE)
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      if (Send(who,@GetGender) = GENDER_FEMALE)
      {
         Send(self,@SetMood,#new_mood=piMood-2);
      }

      return;
   }

   SetForSale()
   {
      /// Madelia favors more regal clothes:  deep, luxurious colors and 
      /// full length robes and shirts.

      plFor_sale = [
         [ 
            Create(&Robe,#color=XLAT_TO_PURPLE),
            Create(&Robe,#color=XLAT_TO_GRAY),
            Create(&Robe,#color=XLAT_TO_BLACK),
            Create(&Robe,#color=XLAT_TO_BLUE),
            Create(&Robe,#color=XLAT_TO_RED),
            Create(&Robe,#color=XLAT_TO_SKIN4),
            Create(&Robe,#color=XLAT_TO_DGREEN),

            Create(&ShortSkirt,#color=XLAT_TO_GRAY),
            Create(&Skirt,#color=XLAT_TO_GRAY),
            Create(&PantsC,#translation=PT_GRAY_TO_DGREEN),
            Create(&PantsC,#translation=PT_GRAY_TO_KBGREEN),
            Create(&PantsC,#translation=PT_GRAY_TO_LSKY),
            Create(&PantsC,#translation=PT_GRAY_TO_DBLUE),
            Create(&PantsC,#translation=PT_GRAY_TO_KPURPLE),
            Create(&PantsC,#translation=PT_GRAY_TO_KGOLD),
            Create(&PantsC,#translation=PT_GRAY_TO_KBLUE),
            Create(&PantsC,#translation=PT_GRAY_TO_KBBLUE),
            Create(&PantsC,#translation=PT_GRAY_TO_KRED),
            Create(&PantsC,#translation=PT_GRAY_TO_KORANGE),

            Create(&PantsA,#color=XLAT_TO_BLUE),
            Create(&PantsA,#color=XLAT_TO_PURPLE),
            Create(&PantsA,#color=XLAT_TO_SKY),
            Create(&PantsA,#color=XLAT_TO_DGREEN),

            Create(&Shirt,#color=XLAT_TO_SKY),
            Create(&Shirt,#color=XLAT_TO_PURPLE),
            Create(&Shirt,#color=XLAT_TO_DGREEN),
            Create(&Shirt,#color=XLAT_TO_BLUE),

            Create(&ReagentBag)
         ],         
         $,
         [
            SID_CRAFTMAGICROBES
         ],
         $];

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if sid = SID_CRAFTMAGICROBES
      {
         if NOT Send(self,@SpecialTeachChecks,#report=TRUE,#who=who,#num=sid)
         {
            return FALSE;
         }
      }

      propagate;
   }

   CanAddSpell(who=$,num=0,report=FALSE)
   {
      if NOT Send(self,@SpecialTeachChecks,#report=report,#who=who,#num=num)
      {
         return FALSE;
      }

      propagate;
   }

   SpecialTeachChecks(who=$,num=0,report=FALSE)
   {
      local i, oSpell, iSchoolRequired, bFound;

      if who = $ OR num = 0
      {
         Debug("Called SpecialTeachChecks with bad parameters!");

         return FALSE;
      }

      oSpell = Send(SYS,@FindSpellByNum,#num=num);

      if NOT Send(oSpell,@IsAccessible)
      {
         return FALSE;
      }

      if Send(who,@HasSpell,#num=num)
      {
         if report
         {
            Post(self,@SayToOne,#target=who,
                  #message_rsc=Batail_already_have_spell,
                  #parm1=Send(oSpell,@GetName));
         }

         return FALSE;
      }

      bFound = FALSE;
      iSchoolRequired=0;

      if num = SID_CRAFTMAGICROBES
      {
         iSchoolRequired = SS_FAREN;
      }

      if iSchoolRequired = 0
      {
         return TRUE;
      }

      // Special checks for high level spells: need a high level spell in 
      // a particular school.
      foreach i in Send(who,@GetSpellList)
      {
         oSpell = Send(SYS,@FindSpellByNum,
                        #num=Send(who,@DecodeSpellNum,#compound=i));
         if Send(oSpell,@GetLevel) > 4
            AND Send(oSpell,@GetSchool) = iSchoolRequired
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound = FALSE
      {
         if report
         {
            Post(self,@SayToOne,#target=who,#message_rsc=Batail_not_ready);
         }

         return FALSE;
      }

      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
