// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
BergInnkeeper is BergTown

constants:

   include blakston.khd
   
resources:

   include berginnk.lkod

   BergInnkeep_name_rsc = "Bastiane d'Ile"
   BergInnkeep_icon_rsc = berginnk.bgf
   BergInnkeep_desc_rsc = \
      "The barkeeper seems out of place in his own pub.  His fine clothes are "
      "worn and were likely tailored for him in younger, slimmer days.  As you "
      "approach, the strong smell of booze fills the air while he rinses a "
      "pitcher with trembling hands and glassy eyes."

   Berg_entry_welcome = \
      "Hey hey, %s%s!  C-c-come on in, gnihihihi! Drink! Driiiink, I say!  "
      "S-sit down and share <hick> your s-stories, he-hee-heee!"

classvars:

   vrName = BergInnkeep_name_rsc
   vrIcon = BergInnkeep_icon_rsc
   vrDesc = BergInnkeep_desc_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_NOMOVE \
      | MOB_LISTEN | MOB_RECEIVE | MOB_TEACHER | MOB_COND_SELLER
   viOccupation = MOB_ROLE_INNKEEPER
   viMerchant_markup = MERCHANT_DISCOUNT

properties:

messages:

   SetForSale()
   {
      plFor_sale = [ 
         [
            Create(&LoadoutScroll,#labelled=TRUE),
            Create(&SetScroll,#labelled=TRUE),
            Create(&Cheese,#number=3),
            Create(&Mug)
         ],
      $,$,$];

      return;
   }

   UserEntered(who=$)
   {
      local iVar;

      Post(who,@SomeoneSaid,#what=self,#string=Berg_entry_welcome,
            #type=SAY_RESOURCE,
            #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      iVar = Send(who,@GetHomeRoom);

      if iVar = RID_JAS_INN
      {
         Send(self,@SetMood,#new_mood=piMood-2);
      }

      if iVar = RID_COR_INN
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      iVar = Send(who,@GetFaction);

      if iVar = FACTION_DUKE
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }
      else if iVar = FACTION_PRINCESS
      {
         Send(self,@SetMood,#new_mood=piMood-3);
      }
      else if iVar = FACTION_REBEL
      {
         Send(self,@SetMood,#new_mood=piMood-10);
      }

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
