// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
BergMerchant is BergTown

constants:

   include blakston.khd

   // How many items do we hold at the start?
   NUMBER_STARTING_ITEMS = 5

   // What's the maximum items we'll hold?
   MAX_FORSALE = 5

   //  Base is about 2 hours (in seconds).
   INVENTORY_DELAY = 2 * 60 * 60

resources:

   include bergmerch.lkod

   bergmerchant_name_rsc = "Luisane d'Ile"
   bergmerchant_icon_rsc = bergmerch.bgf
   bergmerchant_desc_rsc = \
      "A gaunt woman whose nervous eyes follow every customer who enters.  "
      "The cave's dim light conceals much of her weathered features, while the "
      "scent of exotic herbs lingers in the air.  Her tired smile hints at a "
      "beauty and youth now faded by hardship."

   BergMerch_entry_welcome = \
      "Welcome to my humble herb shop.  If you need rest, my brother "
      "Bastiane runs the inn nearby."

   BergMerchant_AlreadyHaveOne_rsc = \
      "Can't you see, I already have one of those."
   BergMerchant_Full_rsc = "You nyrphling, I cannot carry no more."
   BergMerchant_Not_Interested = "As if I'm interested in that."

   BergMerchant_unwanted_rsc = "You're wasting my time."
   BergMerchant_sold_rsc = "I'll see you again soon."

classvars:

   vrName = bergmerchant_name_rsc
   vrIcon = bergmerchant_icon_rsc
   vrDesc = bergmerchant_desc_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN \
      | MOB_NOMOVE | MOB_BUYER | MOB_SELLER \
      | MOB_TEACHER
   viOccupation = MOB_ROLE_MERCHANT
   viMerchant_markup = MERCHANT_FLAT
   viGender = GENDER_FEMALE

   // For each new item we only have limited inventory
   viBaseStock = 50
   viWindrootMax = 20

   // We sell what other people have given us
   vbSellFromInventory = TRUE

properties:

   vrBuyer_Unwanted = BergMerchant_Not_Interested

   ptNew_Inventory = $

messages:

   Constructor()
   {
      local iInventoryTime;

      iInventoryTime = (INVENTORY_DELAY * Random(90,110)) / 100;
      // Convert to ms.
      iInventoryTime *= 1000;
      ptNew_Inventory = CreateTimer(self,@AddNewInventoryToSell,iInventoryTime);

      propagate;
   }

   Delete()
   {
      if ptNew_Inventory <> $
      {
         DeleteTimer(ptNew_Inventory);
         ptNew_Inventory = $;
      }

      propagate;
   }

   AddNewInventoryToSell()
   {
      local oInventory, lForSale, i, bDuplicate, iInventoryTime;

      lForSale = First(plFor_Sale);

      if Length(lForSale) < MAX_FORSALE
      {
         oInventory = Send(self,@PickInventory);
         bDuplicate = FALSE;

         foreach i in lForSale
         {
            if Send(i,@GetName) = Send(oInventory,@GetName)
            {
               bDuplicate = TRUE;
               Send(oInventory,@Delete);

               break;
            }
         }

         if NOT bDuplicate
         {
            lForSale = Cons(oInventory,lForSale);
            SetFirst(plFor_Sale, lForSale);
         }
      }

      iInventoryTime = (INVENTORY_DELAY * Random(90,110)) / 100;
      // Convert to ms.
      iInventoryTime *= 1000;
      ptNew_Inventory = CreateTimer(self,@AddNewInventoryToSell,iInventoryTime);

      return;
   }

   SetForSale()
   {
      local i, oInventory, lForSale;

      oInventory = Create(&Snack,#number=viWindrootMax);
      lForSale = [ oInventory ];
      Send(self,@NewHold,#what=oInventory);

      for (i = NUMBER_STARTING_ITEMS; i > 0; --i)
      {
         oInventory = Send(self,@PickInventory);

         if IsClass(oInventory,&Snack)
         {
            Send(oInventory,@Delete);
            oInventory = Create(&Snack,#number=viWindrootMax);
         }

         lForSale = Cons(oInventory,lForSale);
         Send(self,@NewHold,#what=oInventory);
      }

      plFor_sale = [lForSale, $, $, $];

      return;
   }

   PickInventory()
   {
      local oInventory,iRandom,iRan,lInventoryList;

      iRandom = Random(1,100);

      if (iRandom < 12)
      {
         lInventoryList = [  &YrxlSap,
                             &BlueDragonScale,
                             &DarkAngelFeather
                          ];
      }
      else if (iRandom < 25)
      {
         lInventoryList = [  &PurpleMushroom,
                             &Ruby,
                             &PolishedSeraphym,
                             &ShamanBlood,
                             &Snack
                          ];
      }
      else if (iRandom < 40)
      {
         lInventoryList = [  
                             &KriipaClaw,
                             &Firesand,
                             &UncutSeraphym
                          ];
      }
      else if (iRandom < 75)
      {
         lInventoryList = [  &BlueMushroom,
                             &OrcTooth,
                             &Sapphire,
                             &Solagh,
                             &RainbowFern,
                             &WebMoss,
                             &FairyWing,
                             &EntrootBerry
                          ];
      }
      else 
      {
         lInventoryList = [  &Herbs,
                             &Elderberry,
                             &Snack,
                             &Mushroom,
                             &Emerald,
                             &RedMushroom
                          ];
      }

      iRan = Random(1,Length(lInventoryList));
      oInventory = Create(Nth(lInventoryList,iRan),#number=viBaseStock);

      return oInventory;
   }

   ObjectDesired(what=$)
   {
      local i;

      // TODO: duplicated in other merchants
      if (NOT IsClass(what,&NumberItem)
         OR IsClass(what,&Money)
         OR IsClass(what,&Token)
         OR IsClass(what,&Totem)
         OR Send(what,@IsItemType,#type=ITEMTYPE_SPECIAL)
         OR Send(what,@HasAttribute,#ItemAtt=IA_BONDED))
      {
         vrBuyer_Unwanted = BergMerchant_Not_Interested;

         return FALSE;
      }

      if Length(First(plFor_Sale)) > MAX_FORSALE
      {

         vrBuyer_Unwanted = BergMerchant_AlreadyHaveOne_rsc;

         return FALSE;
      }

      foreach i in First(plFor_Sale)
      {
         if GetClass(i) = GetClass(what)
         {
            if Send(what,&NumberItem)
            {
                return TRUE;
            }

            vrBuyer_Unwanted = BergMerchant_unwanted_rsc;

            return FALSE;
         }
      }

      return TRUE;
   }

   AcceptOffer()
   "Transfer the purchased merchandise - MOB_BUYER"
   {
      local i,j,oHolder,lTake_items, bHadSomeNum, lForSale;

      lForSale = First(plFor_Sale);
      // Set the stuff where the user can get to it.
      oHolder = Send(SYS,@GetSystemHolder2);
      foreach i in plOffer_items
      {
         Send(oHolder,@NewHold,#what=i);
      }

      // Take stuff the user put there.
      oHolder = Send(SYS,@GetSystemHolder1);
      foreach lTake_items in [ Send(oHolder,@GetHolderActive),
                           Send(oHolder,@GetHolderPassive) ]
      {
         foreach i in lTake_items
         {
            Send(self,@NewHold,#what=i);
            if Send(i,&NumberItem)
            {
               bHadSomeNum = FALSE;
               foreach j in lForSale
               {
                  if GetClass(j) = GetClass(i)
                  {
                     bHadSomeNum = TRUE;
                     Send(j,@AddNumber,#number=Send(i,@GetNumber));
                     // We don't need 'i' any more, delete it.
                     Send(i,@Delete);

                     break;
                  }
               }
               if NOT bHadSomeNum
               {
                  lForSale = Cons(i,lForSale);
               }
            }
            else
            {
               // If this is a spell item, then stop the go bad timer.
               if IsClass(i,&SpellItem)
               {
                  Send(i,@StopGoBadTimer);
               }

               lForSale = Cons(i,lForSale);
            }
         }
      }

      // kill the cancel offer timer
      SetFirst(plFor_Sale, lForSale);
      Send(self,@OfferCanceled);

      return TRUE;
   }

   ItemBoughtHook(buyer = $)
   {
      Post(poOwner,@SomeoneSaid,#what=self,
            #string=BergMerchant_sold_rsc,#type=SAY_RESOURCE);

      return;
   }

   UserEntered(who=$)
   {
      if Send(who,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
      {
         Send(self,@SetMood,#new_mood=piMood+2);
      }

      Post(who,@SomeoneSaid,#what=self,#string=BergMerch_entry_welcome,
            #type=SAY_RESOURCE);

      propagate;
   }

   CanAddSkill(who=$,num=0)
   "We require that a quest be done before one of the skills can be learned"
   {
      if NOT Send(self,@HasDoneLearnQuest,#who=who)
      {
         return FALSE;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
