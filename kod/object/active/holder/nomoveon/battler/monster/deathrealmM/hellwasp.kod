// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
HellWasp is DeathRealmM

constants:

   include blakston.khd
   POISON_CHANCE = 10
   POISON_DURATION = 180000   //// in milliseconds
   POISON_LOSSRATE = 5000    //// in health points * 10^-4 / second
   
resources:
   
   hellwasp_name_rsc = "hell wasp"
   hellwasp_icon_rsc = hellwasp.bgf
   hellwasp_desc_rsc = \
      "This huge creature drives fear into the heart of anyone  "
      "who comes across it. They are known to be much more  "
      "violent than friendly. Their potent poison will "
      "grip at your soul."


   hellwasp_dead_icon_rsc = hellwaspX.bgf
   hellwasp_dead_name_rsc = "slain hell wasp"

   hellwasp_sound_hit = hellwasp_att.mp3
   hellwasp_sound_miss = hellwasp_att.mp3
   hellwasp_sound_death = hellwasp_death.mp3
   hellwasp_sound_aware = hellwasp_miss.mp3

classvars:

   vrName = hellwasp_name_rsc
   vrIcon = hellwasp_icon_rsc
   vrDesc = hellwasp_desc_rsc
   vrDead_icon = hellwasp_dead_icon_rsc
   vrDead_name = hellwasp_dead_name_rsc

   viTreasure_type = TID_HELL_WASP

   viSpeed = SPEED_VERY_FAST

   vrSound_hit = hellwasp_sound_hit
   vrSound_miss = hellwasp_sound_miss
   vrSound_aware = hellwasp_sound_aware
   vrSound_death = hellwasp_sound_death

properties:

   piAnimation = ANIM_IDLE

   piDrawEffectFlag = OF_BOUNCING

   pbIsFlying = TRUE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_ACID, 80 ],
                        [ATCK_WEAP_NERUDITE, -10 ],
                        [ATCK_WEAP_NONMAGIC, 80 ] ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_IDLE;

      return;
   }

   HitSideEffect(what = $, who = $)
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
               #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);
      
         if who <> $
         {
            Send(who,@MsgSendUser,#message_rsc=dspider_illusion_poisoned,
                  #parm1=Send(what,@GetDef),#parm2=Send(what,@GetName),
                  #parm3=Send(what,@GetHisHer));
         }
      }

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_ONCE);
      AddPacket(4,400,2,8,2,10,2,1);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,5,2,7);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,240,2,8,2,10,2,1);

         return;
      }

	  if piAnimation = ANIM_IDLE
	  {
			AddPacket(1,ANIMATE_CYCLE,4,100,2,1,2,4);

			// Send animation info to user.  Default is no animation, display with
	  }     //  group 1.
	  else
	  {
			AddPacket(1, ANIMATE_NONE, 2, 1);
	  }

		return;
		
   }

end
////////////////////////////////////////////////////////////////////////////////
