// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
FrenzySprout is DeathRealmM

constants:

   include blakston.khd

resources:
   
   frenzysprout_name_rsc = "frenzy sprout"
   frenzysprout_icon_rsc = frenzy.bgf
   frenzysprout_desc_rsc = \
      "When angered, this sprout extends to 15 feet tall before it "
      "attacks its prey. Its powerful jaws can rip through the strongest "
      "armor and chew through deathscale armor with ease. This sprout is"
      "a close relative to the crusher sprout. This particular species "
      "is known for its brutal bites. It carries no venom, but can "
      "easily rip you in half. "

   frenzysprout_dead_icon_rsc = frenzyX.bgf
   frenzysprout_dead_name_rsc = "slain frenzy sprout"

   frenzysprout_sound_hit = frenzysprout_att.mp3
   frenzysprout_sound_miss = frenzysprout_att.mp3
   frenzysprout_sound_death = frenzysprout_death.mp3
   frenzysprout_sound_aware = frenzysprout_miss.mp3

classvars:

   vrName = frenzysprout_name_rsc
   vrIcon = frenzysprout_icon_rsc
   vrDesc = frenzysprout_desc_rsc
   vrDead_icon = frenzysprout_dead_icon_rsc
   vrDead_name = frenzysprout_dead_name_rsc
   viSpeed = SPEED_AVERAGE

   viTreasure_type = TID_FRENZY_SPROUT

   vrSound_hit = frenzysprout_sound_hit
   vrSound_miss = frenzysprout_sound_miss
   vrSound_aware = frenzysprout_sound_aware
   vrSound_death = frenzysprout_sound_death

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_ACID, 80 ],
                        [ATCK_WEAP_NERUDITE, -10 ],
                        [ATCK_WEAP_NONMAGIC, 80 ] ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_ONCE);
      AddPacket(4,400,2,4,2,6,2,1);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,200,2,1,2,3);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,1,2,4,2,1);

         return;
      }

      // if no body animation
      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
