// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
BergGrave is MonsterRoom

constants:

   include blakston.khd

   SECTOR_DOOR = 1
   SEC_PRISM_ZONE = 99

resources:

   include berg_grave.lkod

   room_berg_grave = berg_grave.roo
   room_name_berg_grave = "Clandestine Tombs"

   berg_grave_music = ksong.ogg

   berg_grave_updoor_sound = doorrsup.ogg
   berg_grave_downdoor_sound = doordown.ogg
   berg_grave_no_statues_sound = lich_awr.ogg

   berg_grave_voice_enter = \
      "~kAn eerie voice echoes, \"~I...the life of the dead is placed in the "
      "memory of the living...~k\""
   berg_grave_voice_door_locked = \
      "~kAn eerie voice echoes, \"~I...I always felt it was unpleasant to be "
      "locked out; but it might be worse, perhaps, to be locked in..."
   berg_grave_voice_door_unlocked = \
      "~kAn eerie voice echoes, \"~I...there's something about the shadows... "
      "...what lurks in them depends a lot on who is peering into them..."
   berg_grave_voice_no_statues = \
      "~kAn eerie voice echoes, \"~I~...they will discard you, too..."

   berg_grave_projectile = lightnin.bgf
   berg_grave_prism_wav = portal.ogg

classvars:

   vrName = room_name_berg_grave

   viTeleport_row = 9
   viTeleport_col = 5

   viWeatherZone = WEATHER_ZONE_BERG
   //viRegion = REGION_BERG

   viSeconds_til_close = 10

   vrPrismProjectile = berg_grave_projectile

   vbIsExpansionRoom = TRUE

properties:

   viTerrain_type = TERRAIN_CASTLE | TERRAIN_NECROPOLIS

   prRoom = room_berg_grave
   piRoom_num = RID_BERG_GRAVE
   prMusic = berg_grave_music

   piBaseLight = LIGHT_DARK

   piOutside_factor = OUTDOORS_NONE

   piInit_count_min = 4
   piInit_count_max = 6

   piMonster_count_max = 10

   pbDoorOpen = FALSE

   // What locations do we want to spawn states at?  Each element is a
   //  [row, col, finerow, finecol, direction] triplet.
   plStatueLocations = $
   plBraziers = $

messages:

   CreateStandardExits()
   {
      plEdge_Exits = $;

      plEdge_Exits = Cons([ LEAVE_SOUTH, RID_BERG_LEADER_HALL, 14, 42, ROTATE_90 ], plEdge_exits);

      propagate;
   }

   Constructed()
   {
      //plMonsters = [ [&LostSoul, 100] ];

      // [row, col, finerow, finecol, angle]
      plStatueLocations = [ [ 9, 15, 28,  0, ANGLE_NORTH],
                            [ 9, 22, 28, 32, ANGLE_NORTH],
                            [14, 10, 22, 12, ANGLE_NORTH],
                            [14, 16, 54, 12, ANGLE_NORTH],
                            [14, 18, 55, 56, ANGLE_NORTH],
                            [14, 21, 55, 28, ANGLE_NORTH],
                            [14, 33,  0, 44, ANGLE_WEST],
                            [ 8, 30, 40, 28, ANGLE_NORTH],
                            [ 4, 30, 24, 28, ANGLE_SOUTH],
                            [ 6, 28, 32, 36, ANGLE_EAST],
                            [ 6, 32, 32, 24, ANGLE_WEST]
                          ];

      propagate;
   }

   SetMonsterGenPoints()
   {
      plGenerators = [ [ 5, 15], [ 8, 16], [ 6, 19],
                       [ 8, 21], [ 8, 23], [13, 16],
                       [15, 20], [14, 22], [13, 27],
                       [15, 31], [13, 33], [10, 28],
                       [ 8, 32], [ 5, 27], [ 2, 29],
                       [ 3, 33], [ 7, 35]
                     ];

      return;
   }

   CreateStandardObjects()
   {
      Send(self,@PlaceBraziers);

      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=5,#new_col=49,#fine_row=16,#fine_col=0,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=7,#new_col=49,#fine_row=48,#fine_col=0,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=4,#new_col=9,#fine_row=16,#fine_col=0,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=4,#new_col=10,#fine_row=16,#fine_col=32,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=11,#new_col=12,#fine_row=0,#fine_col=32,#new_angle=ANGLE_EAST);

      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=1,#new_col=28,#fine_row=32,#fine_col=0,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=1,#new_col=33,#fine_row=32,#fine_col=48,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=11,#new_col=28,#fine_row=8,#fine_col=0,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=11,#new_col=33,#fine_row=8,#fine_col=48,#new_angle=ANGLE_EAST);

      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=6,#new_col=16,#fine_row=36,#fine_col=28);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=6,#new_col=21,#fine_row=36,#fine_col=44);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=10,#new_col=15,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=10,#new_col=22,#fine_row=0,#fine_col=32);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=14,#new_col=10,#fine_row=60,#fine_col=16);

      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=15,#new_col=16,#fine_row=26,#fine_col=12);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=15,#new_col=18,#fine_row=26,#fine_col=56);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=15,#new_col=21,#fine_row=24,#fine_col=28);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=14,#new_col=34,#fine_row=0,#fine_col=16);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=9,#new_col=30,#fine_row=8,#fine_col=28);

      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=6,#new_col=32,#fine_row=32,#fine_col=60);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=3,#new_col=30,#fine_row=52,#fine_col=28);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=20),
         #new_row=6,#new_col=28,#fine_row=32,#fine_col=0);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=60),
         #new_row=6,#new_col=41,#fine_row=32,#fine_col=60);

      propagate;
   }

   Delete()
   {
      plBraziers = $;

      propagate;
   }

   SomethingTryGo(what = $,row = $,col = $)
   {
      if (row = 9 AND (col=7 OR col=8))
        AND pbDoorOpen = FALSE
      {
         Send(what,@MsgSendUser,#message_rsc=berg_grave_voice_door_locked);
         return TRUE;
      }

      propagate;
   }

   OpenDoor()
   {
      Send(self,@SomethingWaveRoom,#wave_rsc=berg_grave_updoor_sound);

      Send(self,@SomeoneSaid,#type=SAY_MESSAGE,#string=berg_grave_voice_door_unlocked,#what=self);

      // Door
      Send(self,@SetSector,#sector=SECTOR_DOOR,#animation=ANIMATE_FLOOR_LIFT,
           #height=208,#speed=30);

      pbDoorOpen = TRUE;

      return;
   }

   CloseDoor()
   {
      // Door
      Send(self,@SetSector,#sector=SECTOR_DOOR,#animation=ANIMATE_FLOOR_LIFT,
           #height=272,#speed=150);

      pbDoorOpen = FALSE;
      return;
   }

   CanHavePlayerPortal()
   {
      // Don't allow portals into this room, bypassing puzzle
      return FALSE;
   }

   FirstUserEntered(what = $)
   {
      Send(self,@ResetBraziers);
      Send(self,@PlaceStatues);
      Send(self,@CloseDoor);

      propagate;
   }

   SomethingKilled(what = $,victim = $)
   {
      if IsClass(victim,&LivingStatue)
         AND Send(self,@FindHoldingActive,#class=&LivingStatue,#sequence=2) = $
      {
         Send(self,@CloseDoor);
         Send(self,@SpawnVitkhul);
      }

      propagate;
   }

   PlaceStatues()
   {
      local oStatue, lLocation;

      // Don't do anything if there's already statues here.
      if Send(self,@FindHoldingActive,#class=&LivingStatue) <> $
      {
         return;
      }

      foreach lLocation in plStatueLocations
      {
         oStatue = Create(&LivingStatue);

         Send(self,@NewHold,#what=oStatue,
            #new_row=Nth(lLocation,1),#new_col=Nth(lLocation,2),
            #fine_row=Nth(lLocation,3),#fine_col=Nth(lLocation,4));
         Send(self,@SomethingTurned,#what=oStatue,#new_angle=Nth(lLocation,5));
      }

      Send(self,@SomeoneSaid,#type=SAY_MESSAGE,#string=berg_grave_voice_enter,#what=self);
      //plMonsters = [ [&LostSoul, 100] ];

      return;
   }

   PlaceBraziers()
   {
      local lBrazier1, lBrazier2, lBrazier3, lBrazier4, 
            lBrazier5, lBrazier6, lBrazier7;

      lBrazier1 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier1,
            #new_row=8,#new_col=4,#fine_row=0,#fine_col=32,#new_angle=ANGLE_EAST);
      lBrazier2 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier2,
            #new_row=10,#new_col=4,#fine_row=0,#fine_col=32,#new_angle=ANGLE_EAST);
      lBrazier3 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier3,
            #new_row=8,#new_col=6,#fine_row=0,#fine_col=32,#new_angle=ANGLE_EAST);
      lBrazier4 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier4,
            #new_row=10,#new_col=7,#fine_row=32,#fine_col=32,#new_angle=ANGLE_EAST);
      lBrazier5 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier5,
            #new_row=8,#new_col=7,#fine_row=32,#fine_col=32,#new_angle=ANGLE_EAST);
      lBrazier6 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier6,
            #new_row=13,#new_col=6,#fine_row=0,#fine_col=32,#new_angle=ANGLE_EAST);
      lBrazier7 = Create(&BrazierSwitch);
      Send(self,@NewHold,#what=lBrazier7,
            #new_row=13,#new_col=4,#fine_row=4,#fine_col=32,#new_angle=ANGLE_EAST);

      plBraziers = [ lBrazier1, lBrazier2, lBrazier3, lBrazier4, 
                     lBrazier5, lBrazier6, lBrazier7 ];
      return;
   }

   ResetBraziers()
   {
      SendList(plBraziers,0,@SetFlame,#has_flame=FALSE);

      return;
   }

   BrazierLit(what=$,who=$,bIsLit=FALSE)
   {
      Send(self,@ResetBraziers);

      propagate;
   }

   SpawnVitkhul()
   {
      // start spawning Vitkhul
      //plMonsters = [ [&Vitkhul, 30], [&LostSoul, 70] ];

      Send(self,@SomethingWaveRoom,#wave_rsc=berg_grave_no_statues_sound);
      Send(self,@SomeoneSaid,#type=SAY_MESSAGE,#string=berg_grave_voice_no_statues,#what=self);

      return TRUE;
   }

   #region Projectile
   GetProjectileIcon()
   {
      return vrPrismProjectile;
   }

   SendProjectileAnimation()
   {
      // 40ms between animations
      AddPacket(1, ANIMATE_CYCLE, 4, 40, 2, 1, 2, 2);

      return;
   }

   GetProjectileLightColor()
   {
      return LIGHT_BLUE;
   }

   GetProjectileLightFlags()
   {
      // General lighting information.
      return LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC;
   }

   GetProjectileLightIntensity()
   {
      // Medium light radius for projectiles.  Out of 255 levels.
      return 100;
   }

   GetProjectileSpeed()
   {
      return 12;
   }
   #endregion

end
////////////////////////////////////////////////////////////////////////////////
