// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DValley3 is MonsterRoom

constants:

   include blakston.khd

resources:

   include dvalley3.lkod

   room_dvalley3 = dvalley3.roo
   room_name_dvalley3 = "Hopeless Slopes"
   dvalley3_music = dvalley.ogg

classvars:

   vrName = room_name_dvalley3
   viTerrain_type = TERRAIN_ROAD | TERRAIN_HILLS

   viTeleport_row = 6
   viTeleport_col = 22

   viWeatherZone = WEATHER_ZONE_DVALLEY

   vbIsExpansionRoom = TRUE

properties:

   prRoom = room_dvalley3
   piRoom_num = RID_DVALLEY_3
   prMusic = dvalley3_music
   piBaseLight = LIGHT_NICE
   piOutside_factor = 8
   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   piGen_percent = 100
   piInit_count_min = 1
   piInit_count_max = 2
   piMonster_count_max = 5
   
   pbSnowGroundTexture = FALSE
   pbWeatherEffects = FALSE

messages:

   Constructed()
   {
      plMonsters = [ [&DaemonSkeleton, 70], [&Minotaur, 30]  ];

      // Use Random Spawn
      // plGenerators = [ [5, 18], [8, 17], [10, 7] ];

      propagate;
   }
   
   CreateStandardExits()
   {
      plEdge_Exits = $;

      plEdge_Exits = Cons([ LEAVE_NORTH, RID_DVALLEY_4, 24, 17, ROTATE_NONE ], plEdge_exits);  
      plEdge_Exits = Cons([ LEAVE_SOUTH, RID_DVALLEY_2, 2, 19, ROTATE_NONE ], plEdge_exits);

      propagate;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Skull),
           #new_row=10,#new_col=14,#fine_row=48,#fine_col=48,#angle=ANGLE_EAST);      
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_HALFSKULL),
           #new_row=9,#new_col=6,#fine_row=0,#fine_col=0,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Skull),
           #new_row=7,#new_col=17,#fine_row=32,#fine_col=48,#angle=ANGLE_EAST);      
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_HALFSKULL),
           #new_row=3,#new_col=19,#fine_row=32,#fine_col=0,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Skull),
           #new_row=4,#new_col=11,#fine_row=16,#fine_col=32,#angle=ANGLE_EAST);      
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_HALFSKULL),
           #new_row=5,#new_col=22,#fine_row=0,#fine_col=16,#angle=ANGLE_EAST);

      Send(self,@NewHold,#what=Create(&SmokeColumn),
            #new_row=5,#new_col=20,#fine_row=24,#fine_col=12);

      propagate;
   }
   
   FirstUserEntered()
   {
      Post(self,@DoSandstorm);

      propagate;
   }
   
   TryCreateMonster()
   {
      Send(self,@DoSandstorm);

      propagate;
   }

   DoSandstorm()
   {
      local oSandstorm;

      if NOT Send(SYS,@IsPKAllowed)
      {
         return;
      }

      // Don't cast it if no users present.
      if NOT pbUser_in_room
      {
         return;
      }

      oSandstorm = Send(SYS,@FindSpellByNum,#num=SID_SANDSTORM);
      
      if Send(self,@IsEnchanted,#what=oSandstorm)
      {
         return;
      }
      Send(oSandstorm,@CastSpell,#who=self, #lTargets=[self], 
             #iSpellPower=99, #report=FALSE);

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
