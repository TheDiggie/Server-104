// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
GolemDungeon is MonsterRoom

constants:

   include blakston.khd

   MAINTENANCE_DELAY = 6000000  /// (1 hour)
   BOSS_DELAY = 2000
   
   SEC_LAVA = 1
   SEC_LAVA_PIT = 2
   SEC_BARS = 3
   SEC_BRIDGE_A = 10
   SEC_BRIDGE_B = 11
   SEC_BRIDGE_C = 12
   SEC_BRIDGE_D = 13
   SEC_ROOM_FINAL = 20
   SEC_ROOM_CHEST = 21
   
   // Constants for treasure
   // Objects as treasure, format: [ chance, TREASURE_OBJECT, class, number ]
   TREASURE_OBJECT = 1
   // Magic weapon as treasure, format:
   //  [ chance, TREASURE_WEAPON, class, ItemAtt constant ]
   TREASURE_WEAPON = 2

resources:

   include golem_dungeon.lkod

   room_golem_dungeon = golem_dungeon.roo
   room_name_golem_dungeon = "Caverns of Ancient Rock"
   golem_dungeon_music = song10.ogg
   golem_cant_pass = "Your efforts prove fruitless, it holds firm."
   golem_dungeon_lever_desc = \
      "This lever is caked with years' worth of accumulated rust and slime."

   golem_dungeon_lever_sound = fquake.ogg
   golem_dungeon_lava_spash_sound = lavasplash.ogg
   golem_dungeon_wading_sound = frying.ogg

   golem_dungeon_spawn = \ 
      "~BFiery shapes begin to emerge from the molten rock..."
   golem_dungeon_spawn_boss = \
      "~kInferno whispers, \"~r~IWhat's the saying again? Light a man a fire to "
      "keep him warm for a night, but light him ~Bon~B fire...~k\""
      
   golem_dungeon_lore_book = "A Golem's Tale"
   golem_dungeon_lore_text = \
      "I was wandering alone on a gloomy path unsure of which direction to go. "
      "I felt that I was lost and couldn't remember how I had got here... "
      "nor where I was travelling to...  or who I was.\n\n"
      "A young woman suddenly appeared from the shadows.\n\n"
      "\"Are you my maker?\" I asked.\n\n"
      "She grinned and spoke with a soft tone, \"So... what is your third wish?\"\n\n"
      "I was baffled, \"Third wish? How can there be a third wish when I haven't had a first or second one?\"\n\n"
      "\"You've had two wishes already,\" the woman spoke."
      "\"but your second wish was for you to forget everything you know. There is only one wish left now.\"\n\n"
      "\"I see...\" I said hesitantly. \"I have a hard time believing a word "
      "you say, but there's no harm in trying it out:  I wish to know who I truly am!\"\n\n"
      "\"Funny...\" she mused as she snapped her fingers and slowly faded away. \n\n"
      "\"...that you would wish for the same thing twice.\""
      "\n"

   lava_damage0 = "You withstand the lava unscathed."
   lava_damage = \ 
     "Ouch! The lava burns you for ~B~r%i~B~v damage."

classvars:

   vrName = room_name_golem_dungeon
   
   viTerrain_type = TERRAIN_LAVA
   vrWading_Sound = golem_dungeon_wading_sound

   viTeleport_row = 31
   viTeleport_col = 9
   
   viBurnStrength = 2000
   viBurnDuration = 7000

   vbIsExpansionRoom = TRUE

properties:

   ptBossTimer = $
   ptMaintenance = $
   
   pbCanSpawnFinal = TRUE
   pbCanSpawnBoss = TRUE

   poChest = $
   poLever1 = $
   poLever2 = $
   poLever3 = $
   poLever4 = $
   
   plFinalSpawn = $
   plTreasuresList = $

   piInit_count_min = 5
   piInit_count_max = 7
   piGen_percent = 70
   piMonster_count_max = 8

   piLavaDamage = 8
   piLavaBurnChance = 15
   
   prRoom = room_golem_dungeon
   piRoom_num = RID_GOLEM_DUNGEON

   piBaseLight = LIGHT_VERY_DARK
   piOutside_factor = OUTDOORS_NONE

   prMusic = golem_dungeon_music

messages:

#region Create and Delete
   Constructed()
   {
      plMonsters = [ [&EarthElemental, 100] ];

      plGenerators = [ [25, 21], [49, 19], [42, 31], [39, 48], [47, 53],
                       [40, 66], [53, 46], [52, 65], [72, 65] ];

      plFinalSpawn = [ [76, 20], [86, 27],
                       [83, 26], [83, 29] ];
                       
      Send(self,@ResetTreasureList);
      ptMaintenance = CreateTimer(self,@MaintenanceTimer,MAINTENANCE_DELAY);

      propagate;
   }
   
   Delete()
   {
      if ptMaintenance <> $
      {
         DeleteTimer(ptMaintenance);
         ptMaintenance = $;
      }
      
      if ptBossTimer <> $
      {
         DeleteTimer(ptBossTimer);
         ptBossTimer = $;
      }
      
      plTreasuresList = $;

      Send(poChest,@Delete);
      Send(poLever1,@Delete);
      Send(poLever2,@Delete);
      Send(poLever3,@Delete);
      Send(poLever4,@Delete);

      propagate;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=13,#new_col=29,
            #fine_row=0,#fine_col=48);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=13,#new_col=32,
            #fine_row=0,#fine_col=8);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=18,#new_col=37,
            #fine_row=56,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=17,#new_col=38,
            #fine_row=48,#fine_col=48);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=20,#new_col=22,
            #fine_row=8,#fine_col=16);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=35,#new_col=67,
            #fine_row=16,#fine_col=8);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=35,#new_col=69,
            #fine_row=16,#fine_col=0);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=31,#new_col=66,
            #fine_row=32,#fine_col=48);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=69,#new_col=57,
            #fine_row=40,#fine_col=40);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=77,#new_col=68,
            #fine_row=12,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=81,#new_col=18,
            #fine_row=16,#fine_col=16);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=85,#new_col=18,
            #fine_row=48,#fine_col=16);

      poChest = Create(&Chest);
      Send(self,@NewHold,#what=poChest,#new_row=83,#new_col=16,
            #fine_row=32,#fine_col=4);
            
      poLever1 = Create(&Lever,#description=golem_dungeon_lever_desc);
      Send(self,@NewHold,#what=poLever1,#new_row=30,#new_col=32);
      poLever2 = Create(&Lever,#description=golem_dungeon_lever_desc);
      Send(self,@NewHold,#what=poLever2,#new_row=49,#new_col=62);
      poLever3 = Create(&Lever,#description=golem_dungeon_lever_desc);
      Send(self,@NewHold,#what=poLever3,#new_row=28,#new_col=80);
      poLever4 = Create(&Lever,#description=golem_dungeon_lever_desc);
      Send(self,@NewHold,#what=poLever4,#new_row=40,#new_col=73);

      Send(self,@NewHold,#what=Create(&BookPedestal,
         #name=golem_dungeon_lore_book,#text=golem_dungeon_lore_text),
         #new_row=81,#new_col=45,#fine_row=64,#fine_col=50,#new_angle=ANGLE_NORTH);      

      propagate;
   }

   CreateStandardExits()
   {
      plEdge_Exits = $;

      plEdge_Exits = Cons([ LEAVE_NORTH, RID_BERG_8, 32, 49, ROTATE_NONE ], plEdge_exits);

      plExits = Cons([ 80, 23, ROOM_LOCKED_DOOR,
                       golem_cant_pass ],plExits);
      plExits = Cons([ 81, 24, ROOM_LOCKED_DOOR,
                       golem_cant_pass ],plExits);

      propagate;
   }
#endregion Create and Delete
   
#region Treasure
   ResetTreasureList()
   {
      // All chances (first number) should add up to 100.
      plTreasuresList = [ [ 1, TREASURE_OBJECT, &Rose, 1 ],
						        [ 3, TREASURE_OBJECT, &NeruditeBow, 1 ],
                          [ 2, TREASURE_WEAPON, &Scimitar, WA_BLINDER ],
                          [ 2, TREASURE_WEAPON, &Scimitar, WA_PURGER ],
                          [ 2, TREASURE_WEAPON, &Scimitar, WA_PARALYZER ],
                          [ 2, TREASURE_WEAPON, &LongSword, WA_PARALYZER ],
                          [ 3, TREASURE_WEAPON, &LongSword, WA_BLINDER ],
                          [ 1, TREASURE_WEAPON, &MysticSword, WA_PURGER ],
                          [ 1, TREASURE_WEAPON, &MysticSword, WA_PARALYZER ],
                          [ 4, TREASURE_WEAPON, &NeruditeSword, WA_PURGER ],
                          [ 3, TREASURE_WEAPON, &NeruditeSword, WA_PARALYZER ],
                          [ 3, TREASURE_WEAPON, &NeruditeSword, WA_BLINDER ],
                          [ 3, TREASURE_WEAPON, &Axe, WA_BLINDER ],
                          [ 3, TREASURE_WEAPON, &Axe, WA_PARALYZER ],
                          [ 3, TREASURE_OBJECT, &OfferingKraanan, 1 ],
                          [ 3, TREASURE_OBJECT, &OfferingShalille, 1 ],
                          [ 3, TREASURE_OBJECT, &OfferingRiija, 1 ],
                          [ 3, TREASURE_OBJECT, &OfferingQor, 1 ],
                          [ 3, TREASURE_OBJECT, &OfferingFaren, 1 ],
                          [ 2, TREASURE_OBJECT, &OfferingJala, 1 ],
                          [ 2, TREASURE_OBJECT, &Prism, 1 ],
                          [ 3, TREASURE_OBJECT, &TrueLute, 1 ],
                          [ 5, TREASURE_OBJECT, &PurpleMushroom, 500 ],
                          [ 5, TREASURE_OBJECT, &InkyCap, 150 ],
                          [ 5, TREASURE_OBJECT, &BlueDragonScale, 250 ],
                          [ 5, TREASURE_OBJECT, &DarkAngelFeather, 250 ],
                          [ 5, TREASURE_OBJECT, &EntrootBerry, 350 ],
                          [ 5, TREASURE_OBJECT, &Yrxlsap, 150 ],
                          [ 6, TREASURE_OBJECT, &Ruby, 250 ],
                          [ 3, TREASURE_OBJECT, &Shillings, 50000 ]
                        ];

      return;
   }

   DoEndTreasure()
   {
      local iNumPlayers, iChance, iCumulative, i, oObject, oItemAtt;

      iNumPlayers = 2;

      if iNumPlayers > 4
      {
         iNumPlayers = Random(3,5);
      }
      else
      {
         iNumPlayers = Random(1,iNumPlayers+1);
      }

      while iNumPlayers > 0
      {
         iChance = Random(1,100);
         iCumulative = 0;
         i = 1;

         while iCumulative < 100 AND i <= Length(plTreasuresList)
         {
            iCumulative = iCumulative + First(Nth(plTreasuresList,i));
            if iChance <= iCumulative
            {
               if Nth(Nth(plTreasuresList,i),2) = TREASURE_OBJECT
               {
                  // Create Object.
                  oObject = Create(Nth(Nth(plTreasuresList,i),3),
                                   #number=Nth(Nth(plTreasuresList,i),4));
                  // Put in chest.
                  Send(poChest,@NewHold,#what=oObject);
               }
               else
               {
                  if Nth(Nth(plTreasuresList,i),2) = TREASURE_WEAPON
                  {
                     // Create weapon.
                     oObject = Create(Nth(Nth(plTreasuresList,i),3));
                     // Find Itematt.
                     oItemAtt = Send(SYS,@FindItemAttByNum,
                                     #num=Nth(Nth(plTreasuresList,i),4));
                     // Apply Itematt.
                     Send(oItemAtt,@AddToItem,#oItem=oObject);
                     // Put in chest.
                     Send(poChest,@NewHold,#what=oObject);
                  }
               }
               
               iCumulative = 100;
            }

            i = i + 1;
         }

         iNumPlayers = iNumPlayers - 1;
      }

      return;
   }
#endregion Treasure

   MaintenanceTimer()
   {
      if NOT pbuser_in_room
      {
         Send(self,@ResetDungeon);
      }

      ptMaintenance = CreateTimer(self,@MaintenanceTimer,MAINTENANCE_DELAY);

      return;
   }
   
   NewHoldObject(what = $, new_pos = $)
   {
      local iSectorID;

      if (what = $
         OR new_pos = $
         OR NOT IsClass(what,&User))
      {
         propagate;
      }

      iSectorID = Send(self,@GetSectorIDAtLocation,#row=Nth(new_pos,2),
                        #col=Nth(new_pos,3),#fine_row=Nth(new_pos,4),
                        #fine_col=Nth(new_pos,5));
      Post(self,@UserSectorCheck,#who=what,#iSectorID=iSectorID);

      propagate;
   }

   SomethingMoved(what = $)
   {
      Send(self,@UserSectorCheck,#who=what,
            #iSectorID=Send(what,@GetSectorIDAtObject));

      // Special case if monster falls in the lava pit
      if IsClass(what,&Monster)
        AND Send(what,@GetSectorIDAtObject) = SEC_LAVA_PIT
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=golem_dungeon_lava_spash_sound,
               #what=what);
         Post(what,@Delete);
      }

      propagate;
   }

   SomethingPhasedIn(what=$)
   {
      Send(self,@UserSectorCheck,#who=what,
            #iSectorID=Send(what,@GetSectorIDAtObject));

      propagate;
   }

   SomethingSpectatedIn(what=$)
   {
      Send(self,@UserSectorCheck,#who=what,
            #iSectorID=Send(what,@GetSectorIDAtObject));

      propagate;
   }
   
   SomethingChanged(what=$)
   {
      local i, oObj;

      if what = poLever1
         AND Send(poLever1,@GetState) = LEVER_DOWN
      {
         Send(poLever1,@SetStuck,#newstuck=TRUE);
         Send(self,@SetSector,#sector=SEC_BRIDGE_A,#animation=ANIMATE_FLOOR_LIFT,
              #height=344,#speed=15);
         Send(self,@SomethingWaveRoom,#wave_rsc=golem_dungeon_lever_sound,
               #what=poLever1);
         Send(self,@Rumble);
      }
      else if what = poLever2
         AND Send(poLever2,@GetState) = LEVER_DOWN
      {
         Send(poLever2,@SetStuck,#newstuck=TRUE);
         Send(self,@SetSector,#sector=SEC_BRIDGE_B,#animation=ANIMATE_FLOOR_LIFT,
              #height=336,#speed=15);
         Send(self,@SomethingWaveRoom,#wave_rsc=golem_dungeon_lever_sound,
               #what=poLever2);
         Send(self,@Rumble);
      }
      else if what = poLever3
         AND Send(poLever3,@GetState) = LEVER_DOWN
      {
         Send(poLever3,@SetStuck,#newstuck=TRUE);
         Send(self,@SetSector,#sector=SEC_BRIDGE_C,#animation=ANIMATE_FLOOR_LIFT,
              #height=328,#speed=15);
         Send(self,@SomethingWaveRoom,#wave_rsc=golem_dungeon_lever_sound,
               #what=poLever3);
         Send(self,@Rumble);
      }
      else if what = poLever4
         AND Send(poLever4,@GetState) = LEVER_DOWN
      {
         Send(poLever4,@SetStuck,#newstuck=TRUE);
         Send(self,@SetSector,#sector=SEC_BRIDGE_D,#animation=ANIMATE_FLOOR_LIFT,
              #height=312,#speed=15);
         Send(self,@SomethingWaveRoom,#wave_rsc=golem_dungeon_lever_sound,
               #what=poLever4);
         Send(self,@Rumble);
      }

      propagate;
   }

   SomethingKilled(what = $, victim = $)
   {
      if IsClass(victim,&GiantDaemonSkeleton)
      {
         Send(self,@DoEndTreasure);
         Send(poChest,@LockChest,#bLocked=FALSE);
      }
      else if IsClass(victim,&FireElemental)
         AND Send(self,@CountHoldingHowMany,#class=&FireElemental) <= 1
      {
         Send(self,@LowerGates);
      }
   
      propagate;
   }
   
   UserSectorCheck(iSectorID = 0, who = $)
   {
      if (who = $) OR NOT IsClass(who,&Player)
      {
         return;
      }
      else if iSectorID = SEC_LAVA
        OR iSectorID = SEC_LAVA_PIT
      {
         //Send(self,@CookVictim,#what=who);
      }
      else if (iSectorID = SEC_ROOM_FINAL)
      {
          Send(self,@CreateMonstersInFinal);
      }
      else if (iSectorID = SEC_ROOM_CHEST)
        AND ptBossTimer = $
      {
          ptBossTimer = CreateTimer(self,@CreateBoss,BOSS_DELAY);
      }

      return;
   }
   
   CreateMonstersInFinal()
   {
      local i;
      
      if pbCanSpawnFinal
         AND NOT Send(self,@CountHoldingHowMany,#class=&FireElemental)
      {
          foreach i in plFinalSpawn
          {
             Send(self,@NewHold,#what=Create(&FireElemental),
                #new_row=First(i),#new_col=Nth(i,2),#new_angle=ANGLE_NORTH_EAST);
             Send(self,@SomethingWaveRoom,
                #wave_rsc=golem_dungeon_lava_spash_sound,#what=self);
          }
          Send(self,@SomeoneSaid,#type=SAY_MESSAGE,
             #string=golem_dungeon_spawn, #what=self);
          Send(self,@Rumble);
          Send(poChest,@LockChest,#bLocked=TRUE);
          pbCanSpawnFinal = FALSE;
          return TRUE;
      }
      return;
   }
   
   CreateBoss()
   {
      ptBossTimer = $;
      if pbCanSpawnBoss
         AND NOT Send(self,@CountHoldingHowMany,#class=&GiantRatKing)
      {
         Send(self,@NewHold,#what=Create(&GiantRatKing),
             #new_row=84,#new_col=21);
         Send(self,@SomethingWaveRoom,
             #wave_rsc=golem_dungeon_lava_spash_sound,#what=self);
         Send(self,@SomeoneSaid,#type=SAY_MESSAGE,
             #string=golem_dungeon_spawn_boss, #what=self);
         Send(self,@Rumble);
         
         // Spawn another tough creature near the exit
         // Not required to gaining access to the chest
         Send(self,@NewHold,#what=Create(&EarthElementalChampion),
             #new_row=21,#new_col=48);
         pbCanSpawnBoss = FALSE;
      }
      return;
   }
   
   RaiseGates()
   {
      Send(self,@SetSector,#sector=SEC_BARS,
            #animation=ANIMATE_FLOOR_LIFT,#height=296,#speed=30);

      return;
   }
   
   LowerGates()
   {
      Send(self,@SetSector,#sector=SEC_BARS,
            #animation=ANIMATE_FLOOR_LIFT,#height=200,#speed=30);

      return;
   }
   
   ResetDungeon()
   {
      ptMaintenance = $;
      
      if Send(self,@CountHoldingHowMany,#class=&FireElemental) 
         OR Send(self,@CountHoldingHowMany,#class=&GiantRatKing)
      {
          ptMaintenance = CreateTimer(self,@ResetDungeon,MAINTENANCE_DELAY);
          return FALSE;
      }
      else
      {
          Send(self,@RaiseGates);
          Send(poChest,@LockChest,#bLocked=TRUE);
          pbCanSpawnFinal = TRUE;
          pbCanSpawnBoss = TRUE;
          
          Send(poLever1,@SetStuck,#newstuck=FALSE);
          Send(poLever2,@SetStuck,#newstuck=FALSE);
          Send(poLever3,@SetStuck,#newstuck=FALSE);
          Send(poLever4,@SetStuck,#newstuck=FALSE);
          
          if Send(poLever1,@GetState) = LEVER_DOWN
          {
             Send(poLever1,@SwitchLever);
          }

          if Send(poLever2,@GetState) = LEVER_DOWN
          {
             Send(poLever2,@SwitchLever);
          }

          if Send(poLever3,@GetState) = LEVER_DOWN
          {
             Send(poLever3,@SwitchLever);
          }
          
          if Send(poLever4,@GetState) = LEVER_DOWN
          {
             Send(poLever4,@SwitchLever);
          }
          
          Send(self,@SetSector,#sector=SEC_BRIDGE_A,#animation=ANIMATE_FLOOR_LIFT,
              #height=368,#speed=0);
          Send(self,@SetSector,#sector=SEC_BRIDGE_B,#animation=ANIMATE_FLOOR_LIFT,
              #height=392,#speed=0);
          Send(self,@SetSector,#sector=SEC_BRIDGE_C,#animation=ANIMATE_FLOOR_LIFT,
              #height=416,#speed=0);
          Send(self,@SetSector,#sector=SEC_BRIDGE_D,#animation=ANIMATE_FLOOR_LIFT,
              #height=432,#speed=0);
      }
      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
