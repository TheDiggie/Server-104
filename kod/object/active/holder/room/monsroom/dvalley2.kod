// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DValley2 is MonsterRoom

constants:

   include blakston.khd

   SEC_GLYPH_A = 11
   SEC_GLYPH_B = 12

resources:

   include dvalley2.lkod

   room_dvalley2 = dvalley2.roo
   room_name_dvalley2 = "A Bone Dry Wasteland"
   dvalley2_music = dvalley.ogg

   dvalley2_glyth_killed = \
      "~rThe stone glyph echoes, \"...for what purpose did they choose to "
      "slaughter my people? I shall finally close my eyes to this living "
      "nightmare and lay here to rest. The Vitkhul still watch over the ~Broyal "
      "grave~B. Maybe if you play them a ~Bsad song~B, Hakka will remember you..."

classvars:

   vrName = room_name_dvalley2
   viTerrain_type = TERRAIN_ROAD | TERRAIN_HILLS
   
   viTeleport_row = 8
   viTeleport_col = 13

   viWeatherZone = WEATHER_ZONE_DVALLEY

   viGlyph_RespawnTime = 3600000

   vbIsExpansionRoom = TRUE

properties:

   prRoom = room_dvalley2
   piRoom_num = RID_DVALLEY_2
   prMusic = dvalley2_music
   piBaseLight = LIGHT_NICE
   piOutside_factor = 8
   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   piGen_percent = 100
   piInit_count_min = 2
   piInit_count_max = 3
   piMonster_count_max = 7
   
   pbSnowGroundTexture = FALSE
   pbWeatherEffects = FALSE

   poGlyph = $
   ptGlyph_respawn = $

   piTrainingPointsReward = 20

messages:

   Constructed()
   {
      plMonsters = [ [&Minotaur, 100] ];

      // Use Random Spawn
      // plGenerators = [ [12, 8], [5, 10], [9, 16], [14, 21] ];

      propagate;
   }

   CreateStandardExits()
   {
      plEdge_Exits = $;

      plEdge_Exits = Cons([ LEAVE_NORTH, RID_DVALLEY_3, 15, 16, ROTATE_NONE ], plEdge_exits);  
      plEdge_Exits = Cons([ LEAVE_WEST, RID_DVALLEY_1, 13, 25, ROTATE_NONE ], plEdge_exits);


      propagate;
   }
   
   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Skull),
           #new_row=10,#new_col=5,#fine_row=16,#fine_col=32,#angle=ANGLE_EAST);      
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_HALFSKULL),
           #new_row=12,#new_col=20,#fine_row=48,#fine_col=16,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Skull),
           #new_row=2,#new_col=18,#fine_row=16,#fine_col=0,#angle=ANGLE_EAST);      
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_HALFSKULL),
           #new_row=9,#new_col=17,#fine_row=0,#fine_col=16,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Skull),
           #new_row=7,#new_col=22,#fine_row=32,#fine_col=0,#angle=ANGLE_EAST);      
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_HALFSKULL),
           #new_row=3,#new_col=7,#fine_row=32,#fine_col=16,#angle=ANGLE_EAST);

      Send(self,@NewHold,#what=Create(&SmokeColumn),
            #new_row=2,#new_col=16,#fine_row=38,#fine_col=44);

      propagate;
   }
   
   FirstUserEntered()
   {
      Post(self,@DoHeat);

      propagate;
   }
   
   TryCreateMonster()
   {
      // When we try to create a monster, then make sure Heats is up.
      Send(self,@DoHeat);

      propagate;
   }

   DoHeat()
   {
      local oHeat;

      if NOT Send(SYS,@IsPKAllowed)
      {
         return;
      }

      // Don't cast it if no users present.
      if NOT pbUser_in_room
      {
         return;
      }

      oHeat = Send(SYS,@FindSpellByNum,#num=SID_HEAT);

      if Send(self,@IsEnchanted,#what=oHeat)
      {
         return;
      }

      Send(oHeat,@CastSpell,#who=self, #lTargets=[self], 
             #iSpellPower=99, #report=FALSE);

      return;
   }

   Delete()
   {
      poGlyph = $;

      if ptGlyph_respawn <> $
      {
         DeleteTimer(ptGlyph_respawn);
         ptGlyph_respawn = $;
      }

      propagate;
   }

   GetGlyph()
   {
      return poGlyph;
   }

end
////////////////////////////////////////////////////////////////////////////////
