// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
JixaCity is JixaRoom

constants:

   include blakston.khd

   LIGHTS_OFF = 0
   LIGHTS_ON = 1
   LIGHTS_RANDOM = 2

   THEATER_STAGE = 11
   THEATER_BACKGROUND = 12

resources:

   include jixa.lkod

   room_jixa = jixa.roo
   room_name_jixa = "Hidden Village of Jixa"

   jixa_music = jixa.ogg
   wind_sound_jixa = rs_wind.ogg

   jixa_lights_on_command = "lights on"
   jixa_lights_off_command = "lights off"
   jixa_lights_random_command = "lights random"

   JixaTheater_lever_descA = "Control the backdrop"
   JixaTheater_lever_descB = "Control stage platform"
   JixaTheater_lightswitch_icon = neclever.bgf
   JixaTheater_lightswitch_descA = "Master light switch"
   JixaTheater_lightswitch_descB = "Adjust light tempo"

   Jixa_sign_name = "growing plants"
   Jixa_sign_desc = \
      "Rich soil can be found scattered throughout the lands.  "
      "'Offer' any rich soil seeds to grow your own magical plant.  "
      "Optionally while any plant is growing you can also water them.  "
      "Once these plants are fully grown, anyone may harvest them."

classvars:

   vrName = room_name_jixa

   viTeleport_row = 19
   viTeleport_col = 13

   viFlag_row = 24
   viFlag_col = 45

   viPermanent_flags = ROOM_NO_COMBAT | ROOM_LAMPS

   viTerrain_Type = TERRAIN_CITY | TERRAIN_FOREST

   viLightsTimeMax = 2500

   vbIsExpansionRoom = TRUE

properties:

   piRoom_num = RID_JIXA
   prRoom = room_jixa

   prMusic = jixa_music

   piBaseLight = LIGHT_NICE
   piOutside_factor = 8

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   pbSnowGroundTexture = TRUE

   plBraziers = $
   plLights = $

   ptTheaterLights = $
   piLightsTime = 2000 // Amount of time after which the random lights change.

   piLightsSetting = LIGHTS_OFF

   poLever1 = $
   poLever2 = $
   poLever3 = $
   poLever4 = $

messages:

   Delete()
   {
      poLever1 = $;
      poLever2 = $;
      poLever3 = $;
      poLever4 = $;

      plBraziers = $;
      plLights = $;

      if (ptTheaterLights <> $)
      {
         DeleteTimer(ptTheaterLights);
         ptTheaterLights = $;
      }

      propagate;
   }

   CreatePeriodicSounds()
   {
      plPeriodic_sounds = [ wind_sound_jixa ];
      pbPeriodic_sound_random_location = TRUE;

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      plEdge_Exits = Cons([ LEAVE_SOUTH, RID_JIXA_4, 2, 5, ROTATE_NONE ],
                           plEdge_exits);

      plEdge_Exits = Cons([ LEAVE_WEST, RID_JIXA_3, 4, 24, ROTATE_NONE,
                            ROW_IS_LESS_THAN,19 ], plEdge_exits);

      plEdge_Exits = Cons([ LEAVE_WEST, RID_SWAMPTOJIXA, 2, 38, ROTATE_NONE,
                            ROW_IS_GREATER_THAN,20 ], plEdge_exits);

      plExits = Cons([ 18, 29, RID_JIXA_INN, 8, 8, ROTATE_NONE ],plExits);
      plExits = Cons([ 18, 30, RID_JIXA_INN, 8, 8, ROTATE_NONE ],plExits);

      plExits = Cons([ 16, 32, RID_JIXA_INN, 6, 9, ROTATE_NONE ],plExits);

      plExits = Cons([ 32, 5, RID_JIXA_DYE, 4, 2, ROTATE_NONE ],plExits);

      plExits = Cons([ 30, 29, RID_JIXA_HALL, 4, 6, ROTATE_NONE ],plExits);

      plExits = Cons([ 10, 17, RID_JIXA_SMITH, 10, 8, ROTATE_NONE ],plExits);
      plExits = Cons([ 10, 18, RID_JIXA_SMITH, 10, 8, ROTATE_NONE ],plExits);
      plExits = Cons([ 11, 18, RID_JIXA_SMITH, 10, 8, ROTATE_NONE ],plExits);

      propagate;
   }

   CreateLevers()
   {
      if poLever1 = $
      {
         poLever1 = Create(&Lever,#description=JixaTheater_lever_descA);
         Send(self,@NewHold,#what=poLever1,
               #new_row=2,#new_col=40,#fine_row=44,#fine_col=34);
      }

      if poLever2 = $
      {
         poLever2 = Create(&Lever,#description=JixaTheater_lever_descB);
         Send(self,@NewHold,#what=poLever2,
               #new_row=1,#new_col=40,#fine_row=52,#fine_col=34);
      }

      if poLever3 = $
      {
         poLever3 = Create(&Lever,#range=2,
            #icon=JixaTheater_lightswitch_icon,
            #description=JixaTheater_lightswitch_descA);
         Send(self,@NewHold,#what=poLever3,#new_row=1,#new_col=43,
               #fine_row=10,#fine_col=24,#new_angle=ANGLE_SOUTH);
      }

      if poLever4 = $
      {
         poLever4 = Create(&Lever,#range=2,
            #icon=JixaTheater_lightswitch_icon,
            #description=JixaTheater_lightswitch_descB);
         Send(self,@NewHold,#what=poLever4,#new_row=1,#new_col=44,
               #fine_row=10,#fine_col=28,#new_angle=ANGLE_SOUTH);
      }

      return;
   }

   CreateStandardObjects()
   {
      local lObjCoords1, oLight, oTargetGlobe, oNews;

      lObjCoords1 = [ [16,28, 0,34, OO_BARREL],    [21,16,32,58, OO_BUCKET2],
                      [ 9, 6, 8, 2, OO_TALLTREE2], [22,29, 8, 2, OO_MIDTREE1],
                      [21,31, 8, 2, OO_TALLTREE3], [36,32, 8,34, OO_MIDTREE1],
                      [32,11,40,34, OO_MIDTREE4],  [18,35, 8,18, OO_MIDTREE3],
                      [ 8,30,40,34, OO_MIDTREE2],  [ 8,40,40,34, OO_TALLTREE1],
                      [ 8,53,56,18, OO_SHRUBEE1],  [18,39,40,34, OO_SHRUBEE1],
                      [19,41,48, 2, OO_MIDTREE2],  [20,42,56,34, OO_SHRUBEE1],
                      [21,47, 8, 2, OO_SHRUBEE1],  [20,48,16,18, OO_MIDTREE2],
                      [19,49,24,34, OO_SHRUBEE1],  [17,51,24,18, OO_SHRUBEE1],
                      [16,52, 8,10, OO_MIDTREE2],  [14,53,56, 2, OO_SHRUBEE1],
                      [27,40, 8,18, OO_MIDTREE1],    [ 1,43,32,58, OO_DRESS1],
                      [ 1,44,32,58, OO_DRESS2],    [ 3,41,56,2, OO_STOOL_KOCATAN],
                      [27,46,48,58, OO_MIDTREE4],  [ 3,48,56,58, OO_STOOL_KOCATAN]
                    ];
      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords1);

      Send(self,@NewHold,#what=create(&Sign,#desc=Jixa_sign_desc,#name=Jixa_sign_name),
           #new_row=11,#new_col=36,#fine_row=20,#fine_col=44,#angle=ANGLE_SOUTH_WEST);

      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_FIRE,#iIntensity=30),
            #new_row=11,#new_col=9,#fine_row=56,#fine_col=54);
      Send(self,@NewHold,#what=Create(&SmokeColumn),
            #new_row=29,#new_col=32,#fine_row=24,#fine_col=34);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=13,#new_col=22,#fine_row=40,#fine_col=18,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=20,#new_col=26,#fine_row=40,#fine_col=2,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=30,#new_col=25,#fine_row=40,#fine_col=2,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=37,#new_col=4,#fine_row=8,#fine_col=34,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=10,#new_col=33,#fine_row=56,#fine_col=18,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=22,#new_col=34,#fine_row=40,#fine_col=2,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Brazier),
            #new_row=27,#new_col=31,#fine_row=8,#fine_col=2,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&FoodDispenser,#classtype=&Orange),
            #new_row=11,#new_col=15,#fine_row=40,#fine_col=34,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&FoodDispenser,#classtype=&Apple),
            #new_row=31,#new_col=10,#fine_row=8,#fine_col=34,#angle=ANGLE_EAST);

      oNews = Create(&NewsLink,#nid=NID_GAME,#name=news_game_name,#desc=news_game_desc);
      Send(self,@NewHold,#what=oNews,#new_row=22,#new_col=18,#fine_row=52,#fine_col=8);

       //Theater
      oLight = Create(&Brazier);
      Send(self,@NewHold,#what=oLight,#new_row=10,#new_col=42,
            #fine_row=8,#fine_col=18,#new_angle=ANGLE_EAST);
      plBraziers = Cons(oLight,plBraziers);

      oLight = Create(&Brazier);
      Send(self,@NewHold,#what=oLight,#new_row=14,#new_col=43,
            #fine_row=24,#fine_col=2,#new_angle=ANGLE_EAST);
      plBraziers = Cons(oLight,plBraziers);

      oLight = Create(&Brazier);
      Send(self,@NewHold,#what=oLight,#new_row=10,#new_col=47,
            #fine_row=8,#fine_col=50,#new_angle=ANGLE_EAST);
      plBraziers = Cons(oLight,plBraziers);

      oLight = Create(&Brazier);
      Send(self,@NewHold,#what=oLight,#new_row=14,#new_col=47,
            #fine_row=24,#fine_col=2,#new_angle=ANGLE_EAST);
      plBraziers = Cons(oLight,plBraziers);

      // Set braziers off.
      SendList(plBraziers,0,@SetFlame,#has_flame=FALSE);

      oLight = Create(&DynamicLight,#iColor=LIGHT_BRED,#iIntensity=15);
      plLights = Cons(oLight,plLights);
      Send(self,@NewHold,#what=oLight,#new_row=10,#new_col=42,
            #fine_row=8,#fine_col=18);

      oLight = Create(&DynamicLight,#iColor=LIGHT_BBLUE,#iIntensity=15);
      plLights = Cons(oLight,plLights);
      Send(self,@NewHold,#what=oLight,#new_row=14,#new_col=43,
            #fine_row=24,#fine_col=2);

      oLight = Create(&DynamicLight,#iColor=LIGHT_BGREEN,#iIntensity=15);
      plLights = Cons(oLight,plLights);
      Send(self,@NewHold,#what=oLight,#new_row=10,#new_col=47,
            #fine_row=8,#fine_col=50);

      oLight = Create(&DynamicLight,#iColor=LIGHT_BYELLOW,#iIntensity=15);
      plLights = Cons(oLight,plLights);
      Send(self,@NewHold,#what=oLight,#new_row=14,#new_col=47,
            #fine_row=24,#fine_col=2);

      // Set dynamic lights off.
      SendList(plLights,0,@SetSwitch,#bSwitch=OFF);

      oTargetGlobe = Create(&TargetGlobe);
      Send(self,@NewHold,#what=oTargetGlobe,#new_row=5,#new_col=45,
            #fine_row=16,#fine_col=4,#new_angle=ANGLE_SOUTH);
      Send(self,@NewHold,#what=Create(&ViewpointGlobe,#target=oTargetGlobe),
            #new_row=1,#new_col=46,#fine_row=20,#fine_col=4,
            #new_angle=ANGLE_NORTH_EAST);

      Send(self,@CreateLevers);

      propagate;
   }

   NewHold(what = $)
   {
      // Mark the player as having visited the city
      //if IsClass(what,&Player)
      //{
      //   Send(what,@SetPlayerFlag,#flag=PFLAG3_VISITED_JIXA,
      //         #value=TRUE,#flagset=3);
      //}

      propagate;
   }

   SomeoneSaid(what=$,type=$,string=$)
   {
      local i, each_obj;

      if type = SAY_DM
         AND what <> $
         AND IsClass(what,&DM)
      {
         if StringEqual(string,jixa_lights_off_command)
         {
            Send(self,@SetTheaterLights,#value=LIGHTS_OFF);
         }
         else if StringEqual(string,jixa_lights_on_command)
         {
            Send(self,@SetTheaterLights,#value=LIGHTS_ON);
         }
         else if StringEqual(string,jixa_lights_random_command)
         {
            Send(self,@SetTheaterLights,#value=LIGHTS_RANDOM);
         }
      }

      propagate;
   }

   SomethingChanged(what=$)
   {
      local oLever, i;

      if IsClass(what,&Lever)
      {
         if what = poLever1
         {
            if  Send(poLever1,@GetState) = LEVER_DOWN
            {
               Send(self,@SetSector,#sector=THEATER_BACKGROUND,
                     #animation=ANIMATE_FLOOR_LIFT,#height=188,#speed=20);
            }
            else
            {
               Send(self,@SetSector,#sector=THEATER_BACKGROUND,
                     #animation=ANIMATE_FLOOR_LIFT,#height=282,#speed=15);
            }
         }
         else if what = poLever2
         {
            if  Send(poLever2,@GetState) = LEVER_DOWN
            {
               Send(self,@SetSector,#sector=THEATER_STAGE,
                     #animation=ANIMATE_FLOOR_LIFT,#height=250,#speed=15);
            }
            else
            {
                 Send(self,@SetSector,#sector=THEATER_STAGE,
                     #animation=ANIMATE_FLOOR_LIFT,#height=170,#speed=15);
            }
         }
         else if what = poLever3
         {
            if piLightsSetting = LIGHTS_ON
               OR piLightsSetting = LIGHTS_RANDOM
            {
               Send(self,@SetTheaterLights,#value=LIGHTS_OFF);
               piLightsTime = viLightsTimeMax;
            }
            else
            {
               Send(self,@SetTheaterLights,#value=LIGHTS_RANDOM);
            }
         }
         else if what = poLever4
         {
            if piLightsSetting = LIGHTS_ON
            {
               piLightsTime = viLightsTimeMax;
               Send(self,@SetTheaterLights,#value=LIGHTS_RANDOM);
            }

            if piLightsSetting = LIGHTS_RANDOM
               AND piLightsTime <= viLightsTimeMax
            {
               piLightsTime -= 500;
            }

            if piLightsTime < 500
            {
               piLightsTime = 500;
               Send(self,@SetTheaterLights,#value=LIGHTS_ON);
            }
         }
      }

      propagate;
   }

   SetTheaterLights(value=$)
   {
      local oLight, lColors, iIndex;

      if value <> $
      {
         piLightsSetting = value;
      }

      if ptTheaterLights <> $
      {
         DeleteTimer(ptTheaterLights);
         ptTheaterLights = $;
      }

      // Did we get turned off?
      if piLightsSetting = LIGHTS_OFF
      {
         // Make sure each light is off.
         foreach oLight in plLights
         {
            Send(oLight,@SetSwitch,#bSwitch=OFF);
         }
         
         foreach oLight in plBraziers
         {
            Send(oLight,@SetFlame,#has_flame=FALSE);
         }
      }
      else
      {
         lColors = [ LIGHT_BPURPLE, LIGHT_BORANGE, LIGHT_BGREEN, LIGHT_BRED, LIGHT_BBLUE ];

         foreach oLight in plBraziers
         {
            Send(oLight,@SetFlame,#has_flame=TRUE);
         }

         // Are we on?
         if piLightsSetting = LIGHTS_ON
         {
            // Make sure each light is on and set to the original color
            iIndex = 1;

            foreach oLight in plLights
            {
               Send(oLight,@SetLight,#iColor=Nth(lColors,iIndex));
               Send(oLight,@SetSwitch,#bSwitch=ON);
               iIndex = iIndex + 1;
            }
         }
         // Are we in Random EXTREME PARTY MODE(tm)?
         else if piLightsSetting = LIGHTS_RANDOM
         {
            // Make the lights flash and change color
            foreach oLight in plLights
            {
               iIndex = Nth(lColors,random(1,length(lColors)));
               lColors = DelListElem(lColors,iIndex);
               // Switch the light on with a random color
               Send(oLight,@SetLight,#iColor=iIndex);
               Send(oLight,@SetSwitch,#bSwitch=ON);
            }

            // Create a timer to switch them again.
            ptTheaterLights = CreateTimer(self,@TheaterLightsTimer,piLightsTime);
         }
      }

      return;
   }

   TheaterLightsTimer()
   {
      ptTheaterLights = $;

      Send(self,@SetTheaterLights);

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
