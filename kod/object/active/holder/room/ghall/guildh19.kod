// Meridian 59, Copyright 1994-1912 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


//////////////////////////////////////////////////////////////////////////////////
GuildHall19 is GuildHall

constants:

   FOYER_C_DOOR = 1
   FOYER_W_DOOR = 2
   INNER_N_DOOR = 3
   INNER_S_DOOR = 4
   INNER_SE_DOOR = 5
   SECRET_DOOR = 6

   GH19_FOYER_C = 1
   GH19_FOYER_W = 2

   RESET_TIME = 10000
   SECRET_DELAY = 10000

   STATUS_OKAY = 3

   include blakston.khd

resources:

   include guildh19.lkod

   swamphall_knightshield_name = "knight's shield"
   swamphall_knightshield_icon = mtlshld.bgf
   swamphall_knightshield_desc = "A knight's shield."
   swamphall_longsword_name = "long sword"
   swamphall_longsword_icon = sword.bgf
   swamphall_longsword_desc = "A long sword."
   swamphall_platearmor_name = "plate armor"
   swamphall_platearmor_icon = plateamr.bgf
   swamphall_platearmor_desc = "A suit of plate armor."

   room_name_guildh19 = "The Wetland Fortress"
   room_guildh19 = guildh19.roo

classvars:

   vrName = room_name_guildh19

   viTeleport_row = 8
   viTeleport_col = 18

   viNews_row = 7
   viNews_col = 26
   viNews_finerow = 32
   viNews_finecol = 32

   viLever_row = 16
   viLever_col = 14
   viLever_fine_row = 32
   viLever_fine_col = 0

   viInner_teleport_row = 7
   viInner_teleport_col = 24

   viHellPortal_row = 16
   viHellPortal_col = 23
   viHellPortal_angle = ANGLE_NORTH

   viPublicChest_row = 8
   viPublicChest_col = 18
   viPublicChest_angle = ANGLE_WEST

   viQuality = 6

   viTerrain_type = TERRAIN_CASTLE | TERRAIN_GUILDHALL
   viSeconds_til_close = 15

   vbIsExpansionRoom = TRUE

properties:

   prRoom = room_guildh19
   piRoom_num = RID_GUILDH19

   piBaseLight = LIGHT_NICE

   ptFoyerCDoor = $
   ptFoyerWDoor = $
   ptInnerNDoor = $
   ptInnerSDoor = $
   ptInnerSEDoor = $
   ptSecretDoor = $
   plFoyerCDoor = $
   plFoyerWDoor = $

messages:

   Constructor()
   "These mark the exterior door of the guild, which is only openable by "
   "a guild member or an allied guild member."
   {
      plGuild_doors = [ [8,9], [6,3] ];
      //plInternal_Guild_doors =  [ [8, 8, 0], [6, 4, 1] ];

      // These are boxes containing the guild doors, used
      // for removing players from them when they close.
      plFoyerCDoor = [ 800, 900, 887, 912 ];
      plFoyerWDoor = [ 600, 700, 387, 412 ];

      propagate;
   }

   IsMoveOK(old_row = 1, old_col = 1, old_fine_row = 1, old_fine_col = 1,
            new_row = 1, new_col = 1, new_fine_row = 1, new_fine_col = 1,
            who = $)
   {
      // Hardcoded movement check, prevent hacking into hall.
      // Basic requirement door must not be open
      if ptFoyerCDoor = $
         AND ptFoyerWDoor = $
      {
         if Send(self,@InFoyer,#iRow=old_row,#iCol=old_col)
         {
            // We're in the foyer. Check if we're trying to move into the hall.
            if NOT Send(self,@InFoyer,#iRow=new_row,#iCol=new_col)
            {
               // Bad player! Fail the attempt.
               Debug("ALERT!",Send(who,@GetTrueName),who,
                     "tried to move through closed Guild Hall door ",
                     Send(self,@GetName)," from ",old_row,old_col,old_fine_row,
                     old_fine_col," to ",new_row,new_col,new_fine_row,
                     new_fine_col,". Last door opening was ",
                     GetTime() - Send(self,@GetDoorTimeStamp),
                     " seconds ago.");
               return FALSE;
            }
         }
      }

      return TRUE;
   }


   InFoyer(who=$,iRow=$,iCol=$, iFineRow=$, iFineCol=$)
   "Due to the multiple foyers in this guild hall, we return which "
   "foyer 'who' is currently in.  Returns FALSE if not in foyer, a positive "
   "integer otherwise."
   {
      if who <> $
      {
         if Send(who,@GetOwner) <> self
         {
            return FALSE;
         }

         iRow = Send(who,@GetRow);
         iCol = Send(who,@GetCol);
         iFineRow = Send(who, @GetFineRow);
         iFineCol = Send(who, @GetFineCol);
      }

      if (iRow >= 6 AND iRow < 9) AND (iCol >= 9 AND iCol <= 19)
      {
         return GH19_FOYER_C;
      }

      if (iRow >= 2) AND (iRow <= 8) AND (iCol >= 2 AND iCol < 4)
      {
         return GH19_FOYER_W;
      }

      return FALSE;
   }

//   InFoyer(who=$)
//   {
//      return Send(who,@GetSectorIDAtObject) = FOYER_SECTOR_ID;
//   }

   ValidEntry(what=$)
   {

      if Send(self,@InFoyer,#who=what)
      {
         // Since we have multiple foyers, let people enter the appropriate foyer
         return;
      }

      // Superclass teleports people to the One True Foyer(tm) if they enter
      // (or log on) inside when they're not normally allowed to enter.
      propagate;
   }

   SomethingTryGo(what=$,row=$,col=$)
   "Opens the guild outer doors, but only if you're a guildmember!"
   {
      local i, count;

      count = 0;
      foreach i in plGuild_doors
      {
         if row = First(i) AND col = Nth(i,2)
         {
            // check to see if the doors should open
            if piStatus = STATUS_OKAY
               AND NOT Send(self,@CanEnter,#who=what)
            {
               // Send a message saying 'only members and their allies may enter.' 
               Send(what,@MsgSendUser,#message_rsc=guildhall_cant_open,#parm1=vrName);

               return TRUE;
            }
            else
            {
               Send(self,@OpenEntranceDoor,#doorNum=count);

               return TRUE;
            }
         }

         count = count + 1;
      }

      if (row >= 3 AND row <= 4) AND (col >= 7 AND col <= 9) AND ptInnerNDoor = $
      {
         Send(self,@OpenEntranceDoorSound);
         Send(self,@SetSector,#sector=INNER_N_DOOR,#animation=ANIMATE_FLOOR_LIFT,
               #height=896,#speed=100);
         ptInnerNDoor = CreateTimer(self,@CloseInnerNDoor,RESET_TIME);

         return TRUE;
      }

      if (row >= 11 AND row <= 12) AND (col >= 7 AND col <= 9) AND ptInnerSDoor = $
      {
         Send(self,@OpenEntranceDoorSound);
         Send(self,@SetSector,#sector=INNER_S_DOOR,#animation=ANIMATE_FLOOR_LIFT,
               #height=896,#speed=100);
         ptInnerSDoor = CreateTimer(self,@CloseInnerSDoor,RESET_TIME);

         return TRUE;
      }

      if (row >= 13 AND row <= 14) AND (col >= 23 AND col <= 24) AND ptInnerSEDoor = $
      {
         Send(self,@OpenEntranceDoorSound);
         Send(self,@SetSector,#sector=INNER_SE_DOOR,#animation=ANIMATE_FLOOR_LIFT,
               #height=896,#speed=100);
         ptInnerSEDoor = CreateTimer(self,@CloseInnerSEDoor,RESET_TIME);

         return TRUE;
      }

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      //Staircase Exit
      plExits = Cons([ 8, 11, RID_SWAMP1, 58, 26, ROTATE_NONE ],plExits);
      plExits = Cons([ 8, 12, RID_SWAMP1, 58, 26, ROTATE_NONE ],plExits);

      //NE Tower Exit
      plExits = Cons([ 6, 17, RID_SWAMP1, 53, 30, ROTATE_NONE ],plExits);

      //NW Tower Exit
      plExits = Cons([ 6, 11, RID_SWAMP1, 53, 25, ROTATE_NONE ],plExits);

      //Roof Exit
      plExits = Cons([ 7, 13, RID_SWAMP1, 56, 28, ROTATE_NONE ],plExits);

      //Cave Exit
      plExits = Cons([ 4, 3, RID_SWAMP1, 50, 15, ROTATE_NONE ],plExits);
      plExits = Cons([ 4, 2, RID_SWAMP1, 50, 15, ROTATE_NONE ],plExits);
      plExits = Cons([ 3, 2, RID_SWAMP1, 50, 15, ROTATE_NONE ],plExits);
      plExits = Cons([ 3, 3, RID_SWAMP1, 50, 15, ROTATE_NONE ],plExits);

      propagate;
   }

   OpenEntranceDoor(doorNum = $)
   {
      if (doorNum = 0) and ptFoyerCDoor = $
      {
         Send(self,@TimeStampDoor);
         Send(self,@SetSector,#sector=FOYER_C_DOOR,#animation=ANIMATE_FLOOR_LIFT,
               #height=896,#speed=80);
         ptFoyerCDoor = CreateTimer(self,@CloseFoyerCDoor,RESET_TIME);
      }

      if (doorNum = 1) and ptFoyerWDoor = $
      {
         Send(self,@TimeStampDoor);
         Send(self,@SetSector,#sector=FOYER_W_DOOR,#animation=ANIMATE_FLOOR_LIFT,
               #height=896,#speed=80);
         ptFoyerWDoor = CreateTimer(self,@CloseFoyerWDoor,RESET_TIME);
      }

      Send(self,@OpenEntranceDoorSound);

      return;
   }

   CloseFoyerCDoor()
   {
      Send(self,@SetSector,#sector=FOYER_C_DOOR,#animation=ANIMATE_FLOOR_LIFT,
            #height=992,#speed=50);
      Send(self,@CloseEntranceDoorSound);
      ptFoyerCDoor = CreateTimer(self,@ClearFoyerCDoor,1500);

      return;
   }

   CloseFoyerWDoor()
   {
      Send(self,@SetSector,#sector=FOYER_W_DOOR,#animation=ANIMATE_FLOOR_LIFT,
            #height=992,#speed=50);
      Send(self,@CloseEntranceDoorSound);
      ptFoyerWDoor = CreateTimer(self,@ClearFoyerWDoor,1500);

      return;
   }

   CloseInnerNDoor()
   {
      Send(self,@SetSector,#sector=INNER_N_DOOR,#animation=ANIMATE_FLOOR_LIFT,
            #height=992,#speed=50);
      Send(self,@CloseEntranceDoorSound);
      ptInnerNDoor = $;

      return;
   }

   CloseInnerSDoor()
   {
      Send(self,@SetSector,#sector=INNER_S_DOOR,#animation=ANIMATE_FLOOR_LIFT,
            #height=992,#speed=50);
      Send(self,@CloseEntranceDoorSound);
      ptInnerSDoor = $;

      return;
   }

   CloseInnerSEDoor()
   {
      Send(self,@SetSector,#sector=INNER_SE_DOOR,#animation=ANIMATE_FLOOR_LIFT,
            #height=992,#speed=50);
      Send(self,@CloseEntranceDoorSound);
      ptInnerSEDoor = $;

      return;
   }

   ClearFoyerCDoor()
   {
      Send(self,@ClearDoor,#lDoor=plFoyerCDoor);
      ptFoyerCDoor = $;

      return;
   }

   ClearFoyerWDoor()
   {
      Send(self,@ClearDoor,#lDoor=plFoyerWDoor);
      ptFoyerWDoor = $;

      return;
   }

   OpenSecretDoor()
   {
      if ptSecretDoor = $
      {
         Send(self,@SetSector,#sector=SECRET_DOOR,#animation=ANIMATE_FLOOR_LIFT,
               #height=896,#speed=16);
         Send(self,@OpenSecretDoorSound);
         ptSecretDoor = CreateTimer(self,@CloseSecretDoor,SECRET_DELAY);
      }

      return;
   }

   CloseSecretDoor()
   {
      ptSecretDoor = $;
      Send(self,@SetSector,#sector=SECRET_DOOR,#animation=ANIMATE_FLOOR_LIFT,
            #height=984,#speed=16);
      Send(self,@CloseSecretDoorSound);

      return;
   }

   CreateStandardObjects()
   {
      //Braziers
      Send(self,@NewHold,#what=Create(&Brazier),
           #new_row=7,#new_col=14,#fine_row=32,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),
           #new_row=2,#new_col=6,#fine_row=0,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),
           #new_row=13,#new_col=6,#fine_row=0,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),
           #new_row=13,#new_col=26,#fine_row=0,#fine_col=32);

      //Barrels
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_BARREL2),
           #new_row=5,#new_col=30,#fine_row=16,#fine_col=16,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_BARREL2),
           #new_row=4,#new_col=30,#fine_row=32,#fine_col=48,#new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_BARREL2),
           #new_row=7,#new_col=28,#fine_row=0,#fine_col=0,#new_angle=ANGLE_EAST);

      //Decorative Items
      Send(self,@NewHold,#what=Create(&DecoItem,#name=swamphall_knightshield_name,
                                      #icon=swamphall_knightshield_icon,
                                      #desc=swamphall_knightshield_desc),
           #new_row=9,#new_col=10,#fine_row=56,#fine_col=16);
      Send(self,@NewHold,#what=Create(&DecoItem,#name=swamphall_longsword_name,
                                      #icon=swamphall_longsword_icon,
                                      #desc=swamphall_longsword_desc),
           #new_row=9,#new_col=14,#fine_row=56,#fine_col=0);
      Send(self,@NewHold,#what=Create(&DecoItem,#name=swamphall_platearmor_name,
                                      #icon=swamphall_platearmor_icon,
                                      #desc=swamphall_platearmor_desc),
           #new_row=9,#new_col=17,#fine_row=56,#fine_col=48);

      //Misc
      Send(self,@NewHold,#what=Create(&Firepit),
           #new_row=1,#new_col=24,#fine_row=0,#fine_col=48);
      Send(self,@NewHold,#what=Create(&Stool),
           #new_row=3,#new_col=28, #fine_row=0,#fine_col=32,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Stool),
           #new_row=5,#new_col=26, #fine_row=0,#fine_col=32,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Candelabra),#new_row=13,#new_col=30,
           #fine_row=0,#fine_col=4,#angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&QuillPen),
           #new_row=16,#new_col=22,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_POTPLANT),
           #new_row=14,#new_col=26,#fine_row=16,#fine_col=48,#new_angle=ANGLE_EAST);


      //Chests
      Send(self,@NewHold,#what=Create(&WoodenBox),
            #new_row=11,#new_col=28,#fine_row=0,#fine_col=0,
            #new_angle=ANGLE_SOUTH);
      Send(self,@NewHold,#what=Create(&WoodenBox),
            #new_row=11,#new_col=30,#fine_row=0,#fine_col=0,
            #new_angle=ANGLE_WEST);

      propagate;
   }

   Delete()
   {
      if ptSecretDoor <> $
      {
         DeleteTimer(ptSecretDoor);
         ptSecretDoor = $;
      }

      if ptFoyerCDoor <> $
      {
         DeleteTimer(ptFoyerCDoor);
         ptFoyerCDoor = $;
      }

      if ptFoyerWDoor <> $
      {
         DeleteTimer(ptFoyerWDoor);
         ptFoyerWDoor = $;
      }

      if ptInnerNDoor <> $
      {
         DeleteTimer(ptInnerNDoor);
         ptInnerNDoor = $;
      }

      if ptInnerSDoor <> $
      {
         DeleteTimer(ptInnerSDoor);
         ptInnerSDoor = $;
      }

      if ptInnerSEDoor <> $
      {
         DeleteTimer(ptInnerSEDoor);
         ptInnerSEDoor = $;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
