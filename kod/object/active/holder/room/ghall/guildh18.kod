// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


//////////////////////////////////////////////////////////////////////////////////
GuildHall18 is GuildHall

constants:

   LIFT_DELAY = 20000

   SEC_PLATFORM = 50
   SEC_LAVA = 51
   SEC_HALL_INSIDE = 52

   include blakston.khd

resources:

   include guildh18.lkod

   room_name_guildh18 = "The Red Line"
   room_guildh18 = guildh18.roo

   guildhall18_lava_damage0 = "You withstand the lava unscathed."
   guildhall18_lava_damage = \ 
     "Ouch! The lava burns you for ~B~r%i~B~v damage."

   guildhall18_lava_snd = frying.ogg

   guildhall18_entrancedoor_open_sound = shielddn.ogg
   guildhall18_entrancedoor_close_sound = shieldup.ogg

classvars:

   vrName = room_name_guildh18

   viTeleport_row = 40
   viTeleport_col = 40

   viInner_teleport_row = 11
   viInner_teleport_col = 33

   viNews_row = 13
   viNews_col = 31
   viNews_finerow = 32
   viNews_finecol = 32

   viFoyer_north = 10
   viFoyer_south = 16
   viFoyer_west = 20
   viFoyer_east = 27

   viLever_row = 11
   viLever_col = 10
   viLever_fine_row = 0
   viLever_fine_col = 40

   viHellPortal_row = 11
   viHellPortal_col = 34
   viHellPortal_angle = ANGLE_SOUTH

   viPublicChest_row = 44
   viPublicChest_col = 25
   viPublicChest_angle = ANGLE_SOUTH_WEST

   viQuality = 4

   viSeconds_til_close = 16

   viBurnStrength = 2000
   viBurnDuration = 7000

   viTerrain_type = TERRAIN_BADLANDS | TERRAIN_MYSTICAL | TERRAIN_LAVA
   vrWading_Sound = guildhall18_lava_snd
   vrSecretdoor_open_sound = guildhall18_entrancedoor_open_sound
   vrSecretdoor_close_sound = guildhall18_entrancedoor_close_sound

properties:

   prRoom = room_guildh18
   piRoom_num = RID_GUILDH18

   piBaseLight = LIGHT_NICE

   ptSecret = $

   piLavaDamage = 8
   piLavaBurnChance = 15

messages:

   CreateStandardExits()
   {
      plExits = $;

      plExits = Cons([ 55, 34, RID_K5, 4, 24, ROTATE_NONE ],plExits);
      plExits = Cons([ 56, 34, RID_K5, 4, 24, ROTATE_NONE ],plExits);

      propagate;
   }

   Delete()
   {
      if ptSecret <> $
      {
         DeleteTimer(ptSecret);
         ptSecret = $;
      }

      propagate;
   }

   OpenSecretDoor()
   {
      if ptSecret = $
      {
         Send(self,@Rumble);
         Send(self,@ChangeTexture,#id=SEC_PLATFORM,#new_texture=9764,#flags=CTF_FLOOR);
         Send(self,@SetSector,#sector=SEC_PLATFORM,#animation=ANIMATE_FLOOR_LIFT,
              #height=312,#speed=50);
         ptSecret = CreateTimer(self,@CloseSecretDoor,LIFT_DELAY);
         Send(self,@OpenSecretDoorSound);
      }

      return;
   }

   CloseSecretDoor()
   {
      ptSecret = $;
      Send(self,@SetSector,#sector=SEC_PLATFORM,#animation=ANIMATE_FLOOR_LIFT,
           #height=156,#speed=50);
      Send(self,@ChangeTexture,#id=SEC_PLATFORM,#new_texture=9713,#flags=CTF_FLOOR);
      Send(self,@CloseSecretDoorSound);

      return;
   }

   CreateStandardObjects()
   {

      Send(self,@NewHold,#what=Create(&Brazier),
           #new_row=15,#new_col=26,#fine_row=0,#fine_col=0);

      Send(self,@NewHold,#what=Create(&Chest),
           #new_row=13,#new_col=44,#fine_row=0,#fine_col=16,#new_angle=ANGLE_EAST);
  
      Send(self,@NewHold,#what=Create(&Firepit,#iSoundRadius=5),
            #new_row=40,#new_col=36,#fine_row=36,#fine_col=28);

      propagate;
   }

   NewHoldObject(what = $, new_pos = $)
   {
      local iSectorID;

      if (what = $
         OR new_pos = $
         OR NOT IsClass(what,&User))
      {
         propagate;
      }

      iSectorID = Send(self,@GetSectorIDAtLocation,#row=Nth(new_pos,2),
                        #col=Nth(new_pos,3),#fine_row=Nth(new_pos,4),
                        #fine_col=Nth(new_pos,5));
      Post(self,@UserSectorCheck,#who=what,#iSectorID=iSectorID);

      propagate;
   }

   SomethingMoved(what = $)
   {
      Send(self,@UserSectorCheck,#who=what,
            #iSectorID=Send(what,@GetSectorIDAtObject));

      // Special case if monster falls in the lava pit
      if IsClass(what,&Monster)
        AND Send(what,@GetSectorIDAtObject) = SEC_LAVA
      {
         Post(what,@Delete);
      }

      propagate;
   }

   SomethingPhasedIn(what=$)
   {
      Send(self,@UserSectorCheck,#who=what,
            #iSectorID=Send(what,@GetSectorIDAtObject));

      propagate;
   }

   SomethingSpectatedIn(what=$)
   {
      Send(self,@UserSectorCheck,#who=what,
            #iSectorID=Send(what,@GetSectorIDAtObject));

      propagate;
   }

   UserSectorCheck(iSectorID = 0, who = $)
   {
      if (who = $) OR NOT IsClass(who,&Player)
      {
         return;
      }
      else if iSectorID = SEC_LAVA
      {
         //Send(self,@CookVictim,#what=who);
      }
      else if (iSectorID = SEC_PLATFORM)
        AND NOT pbSecretDoorOpen
      {
         if Send(who,@FindUsing,#class=&HunterSword)
         {
            Send(self,@OpenSecretDoor);
         }
      }

      return;
   }

   CanEnter(who=$)
   "This call allows hunters to be able to log off safely here"
   {
      if Send(who,@IsUsingA,#Class=&HunterSword)
      {
         return TRUE;
      }

      propagate;
   }

   InFoyer(who=$,iRow=$,iCol=$,iFineRow=$,iFineCol=$)
   {
      local iSectorID;

      if who <> $
      {
         if Send(who,@GetOwner) <> self
         {
            return FALSE;
         }

         iRow = Send(who,@GetRow);
         iCol = Send(who,@GetCol);
         iFineCol = Send(who,@GetFineCol);
         iFineRow = Send(who,@GetFineRow);

         iSectorID = Send(self,@GetSectorIDAtLocation,#row=Send(who,@GetRow),
                        #col=Send(who,@GetCol),#fine_row=Send(who,@GetFineRow),
                        #fine_col=Send(who,@GetFineCol));
      }

      if iSectorID <> SEC_HALL_INSIDE
      {
         return TRUE;
      }

      return FALSE;
   }

   // This works for standard rectangle guildhall foyers. Other configurations
   // need to override this.
   IsMoveOK(old_row = 1, old_col = 1, old_fine_row = 1, old_fine_col = 1,
            new_row = 1, new_col = 1, new_fine_row = 1, new_fine_col = 1,
            who=$)
   {
      // Hardcoded movement check, prevent hacking into hall.
      // Basic requirement door must not be open
      if ptSecret = $
      {
         if Send(self,@InFoyer,#iRow=old_row,#iCol=old_col,
                  #iFineRow=old_fine_row,#iFineCol=old_fine_col)
         {
            // We're in the foyer. Check if we're trying to move into the hall.
            if NOT Send(self,@InFoyer,#iRow=new_row,#iCol=new_col,
                        #iFineRow=new_fine_Row,#iFineCol=new_fine_col)
            {
               // Bad player! Fail the attempt.
               Debug("ALERT!",Send(who,@GetTrueName),who,
                     "tried to move through closed Guild Hall door ",
                     Send(self,@GetName)," from ",old_row,old_col,old_fine_row,
                     old_fine_col," to ",new_row,new_col,new_fine_row,
                     new_fine_col,". Last door opening was ",
                     GetTime() - Send(self,@GetDoorTimeStamp),
                     " seconds ago.");

               return FALSE;
            }
         }
      }

      return TRUE;
   }

   GetPurchaseValue(who=$)
   "return -1 if this player cannot rent this guild hall."
   {
      return -1;
   }

end
////////////////////////////////////////////////////////////////////////////////
