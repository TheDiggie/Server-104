// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Freezing is RoomEnchantment

constants:

   include blakston.khd

   // Total duration: 10 realtime minutes
   FREEZING_TIME = 300000

resources:

   Freezing_name_rsc = "freezing"
   Freezing_icon_rsc = iicenova.bgf
   Freezing_desc_rsc = \
      "Fills the room with the earth magic of Faren, freezing "
      "all armor causing frostbite. "
   
   Freezing_already_enchanted = "This place already has a freezing enchantment."

   Freezing_on = \
      "Fills the room with the earth magic of Faren, freezing "
      "all armor causing frostbite. "
   Freezing_off = "The armor-freezing field dissipates."
   Freezing_new_entrant = \
      "Faren earth magic fills the room freezing all armor causing frostbite."
   Freezing_kills_you = "Your body freezes into solid ice."
   Freezing_kills_him = "%s%s freezes solid."

   Freezing_hurts_you = \
      "You offer a yelp of pain as your armor freezes to your skin."

   Freezing_sound = fheat.ogg

classvars:

   vrName = Freezing_name_rsc
   vrIcon = Freezing_icon_rsc
   vrDesc = Freezing_desc_rsc

   vrAlreadyEnchanted = Freezing_already_enchanted
   vrRoomEnchKilled = Freezing_kills_you
   vrRoomEnchKiller = Freezing_kills_him
   vrRoomEnchHit = Freezing_hurts_you

   viAttack_spell = ATCK_SPELL_COLD
   viSpell_num = SID_FREEZING
   viSchool = SS_FAREN
   viSpell_level = 3
   viMana = 8

   viHarmful = TRUE
   viNoNewbieOffense = TRUE

   vrSucceed_wav = Freezing_sound
   viChance_To_Increase = 35
   viMeditate_ratio = 30

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&BlueMushroom,1],plReagents);
      plReagents = Cons([&FireSand,1],plReagents);

      return;
   }

   CanPayCosts(who = $)
   {
      if IsClass(Send(who,@GetOwner),&GuildHall)
      {
         Send(who,@MsgSendUser,#message_rsc=spell_bad_location,#parm1=vrName);

         return FALSE;
      }

      propagate;
   }

   CastSpell( who = $, iSpellPower = 0 )
   "Initiation point for the spell."
   {
      local i, oSpell, oRoom, lEnchantments, iPeriod;

      oRoom = Send(who,@GetOwner);

      lEnchantments = Send(oRoom,@GetEnchantmentList);

      foreach i in lEnchantments
      {
         oSpell = Nth(i,2);
         if isClass(oSpell,&Heat)
         {
            Send(oRoom,@RemoveEnchantment,#what=oSpell);
         }
      }

      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#string=Freezing_on,#what=self);

      // ranges from 7 to 17 seconds
      iPeriod = (170-iSpellPower)/10;
      // can't store in greater fineness - value stores state too!
      iPeriod = iPeriod * 1000;

      Send(oRoom,@RoomStartEnchantment,#what=self,#time=iPeriod,
            #state=[iPeriod + iSpellPower, who],#addicon=TRUE,#lastcall=FALSE);

      propagate;
   }

   StartPeriodicEnchantment(who = $, where = $, state = $)
   "Sends enchantment message to room like CastSpell for most room enchantments, "
   "but silent, and done as often as necessary"
   {
      local iNext_time, iCombined, iPower, iPeriod;

      // First, extract the spellpower from the state
      iCombined = First(state);
      iPower = iCombined % 100;
      iCombined = iCombined - iPower;

      // Ranges from 7 to 17 seconds
      iPeriod = (170-iPower)/10;
      // Can't store in greater fineness - value stores state too!
      iPeriod = iPeriod * 1000;
      
      // Global effects of the enchantment
      // State is elapsed time in ms
      iNext_time = iCombined + iPeriod;
      if iNext_time >= FREEZING_TIME
      {
         Send(where,@RoomStartEnchantment,#what=self,#time=FREEZING_TIME-iCombined,
               #state=[FREEZING_TIME + iPower, Nth(state,2)],#addicon=False,
               #lastcall=TRUE);

         return;
      }

      Send(where,@RoomStartEnchantment,#what=self,#time=iPeriod,
            #state=[iNext_time+iPower, Nth(state,2)],#addicon=FALSE,
            #lastcall=FALSE);

      Send(where,@EnchantAllOccupants,#what=self,#iSpellPower=iPower,
            #state=state);

      return;
   }

   StartEnchantmentNewOccupant(who = $,state = $)
   "Called on new occupants of the enchanted room."
   {
      Send(who,@MsgSendUser,#message_rsc=Freezing_new_entrant);
      Send(self,@StartEnchantment,#who=who,#state=state);

      return;
   }

   StartEnchantment(who = $, state = $)
   "Does enchantment effect on one player"
   {
      if (who = $)
      {
         return;
      }

      Send(self,@RoomEnchantmentDamage,#oTarget=who,#state=state);

      return;
   }

   ComputeDamage(who=$,oTarget=$,iDamage=$)
   {
      local i, lUsing;

      lUsing = Send(oTarget,@GetPlayerUsing);

      iDamage = 0;

      foreach i in lUsing
      {
         iDamage = iDamage + Send(i,@GetHeatDamage);
      }

      return iDamage;
   }

   EndSpell(where = $, state = $)
   "Called when spell expires."
   {
      Send(where,@SomeoneSaid,#type=SAY_MESSAGE,#string=Freezing_off,#what=self);

      return;
   }

   EndRoomEnchantment(who = $, state = $)
   "Ends enchantment effect on one player"
   {
      Send(who,@RemoveEnchantment,#what=self);

      return;
   }

   RemoveEnchantment(who = $)
   "Remove enchantment effects on this occupant"
   {
      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
