// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


//////////////////////////S///////////////////////////////////////////////////////
Deflect is PersonalEnchantment

constants:

   include blakston.khd

resources:

   include deflect.lkod

   Deflect_name_rsc = "deflect"
   Deflect_icon_rsc = imirror.bgf
   Deflect_desc_rsc = \
      "Calls upon Kraanan's will to form a protective barrier around the "
      "target which protects them from direct damaging attack magics, but "
      "not from other magics, such as enchantments.  "
      "Requires a vial of solagh and one kriipa claw to cast."

   Deflect_already_enchanted_rsc = "You are already enchanted by deflect."
   deflect_enchantment_rsc = \
      "\n\nYour current %s enchantment will reflect enemy splash and bolt "
      "spells %i%% of the time."
   Deflect_on_rsc = "A magical barrier surrounds you."
   Deflect_success_rsc = "You sense a magical barrier surround %s%s."
   Deflect_off_rsc = "The magical barrier protecting you seems to be waning."

   Deflect_spell_intro = \
      "Kraanan Level 4: Forms a magical barrier around the target, protecting"
      "them from harm of magics that damage directly."

   Deflect_sound = kdeflect.ogg

   Deflect_success_to_victim = \
      "~gYour deflect enchantment bounces %s's spell back onto its caster!"
   Deflect_success_to_caster = \
      "~rA barrier of mystical energy coalesces in front of %s, bouncing "
      "your spell back at you!"
   Deflect_failure_to_victim = \
      "~rYour deflect enchantment attempts to bounce %s's spell back, "
      "but is not strong enough."

classvars:

   viPersonal_ench = True

   viSpellExertion = 4
   vrName = Deflect_name_rsc
   vrIcon = Deflect_icon_rsc
   vrDesc = Deflect_desc_rsc

   vrAlreadyEnchanted = Deflect_already_enchanted_rsc
   vrEnchantment_On = Deflect_On_rsc
   vrEnchantment_Off = Deflect_Off_rsc
   vrSuccess = Deflect_Success_rsc

   viSpell_num = SID_DEFLECT

   vrSpell_intro = Deflect_spell_intro

   viMana = 20
   viCast_time = 0
   viSpell_level = 4
   viSchool = SS_KRAANAN

   viChance_To_Increase = 20
   viMeditate_ratio = 40

   viFlash = FLASH_GOOD_SELF

   vrSucceed_wav = Deflect_sound

properties:

   // The effectiveness of purge on the target spell as a percentage.
   // 100 will cause purge to remove its entire spellpower, 0 will
   // prevent purge from removing this spell.
   viPurgeFactor = 40

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Solagh,1],plReagents);
      plReagents = Cons([&KriipaClaw,1],plReagents);

      return;
   }

   GetStateValue(who=$,iSpellPower=0,Target=$)
   {
      local iReflectOdds;

      iReflectOdds = iSpellPower * 3 / 4;

      return Bound(iReflectOdds,10,75);
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = (60 + iSpellPower) * 1000;

      return iDuration;
   }

   TryDeflect(caster=$,victim=$,oSpell=$)
   "Returns TRUE if the spell is deflected."
   {
      local i, chance;

      if Send(oSpell,@IsHarmful)
         AND Send(oSpell,@GetNumSpellTargets) = 1
         AND IsClass(oSpell,&AttackSpell)
      {
         // Give players a decent chance to improve the spell!
         foreach i in Send(victim,@GetPassiveImprovementList)
         {
            if First(i) = self AND Random(1,3) = 3
            {
               Post(First(i),@ImproveAbility,#who=victim);
            }
         }

         chance = Send(victim,@GetEnchantedState,#what=self);
         if Random(0,100) < chance
         {
            Send(self,@SendMessageTryDeflectSuccess,#victim=victim,#caster=caster);
            return TRUE;
         }

         Send(self,@SendMessageTryDeflectFailure,#victim=victim,#caster=caster);
      }

      return FALSE;
   }

   SendMessageTryDeflectSuccess(victim=$,caster=$)
   {
      Send(victim,@MsgSendUser,#message_rsc=Deflect_success_to_victim,
           #parm1=Send(caster,@GetName));
      Send(caster,@MsgSendUser,#message_rsc=Deflect_success_to_caster,
           #parm1=Send(victim,@GetName));

      return;
   }

   SendMessageTryDeflectFailure(victim=$,caster=$)
   {
      Send(victim,@MsgSendUser,#message_rsc=Deflect_failure_to_victim,
           #parm1=Send(caster,@GetName));

      return;
   }

   GetPotionClass()
   {
      return &DeflectPotion;
   }

   EffectDesc(who=$)
   {
      AddPacket(4,deflect_enchantment_rsc, 4,Send(self,@GetName),
                4,Send(who,@GetEnchantedState,#what=self));
      
      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
