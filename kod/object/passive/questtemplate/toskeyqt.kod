// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
TosKeyQuest is QuestTemplate
// Quest nodes 235, 236, 237, 238
// QNT_ID_TOS_KEY_ONE, QNT_ID_TOS_KEY_TWO,
// QNT_ID_TOS_KEY_THREE, QNT_ID_TOS_KEY_FOUR

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   // Quest description.
   TosKeyQuest_icon_rsc = key7.bgf
   TosKeyQuest_name_rsc = "Key to tos quest"

   // This description is used if there is an error preventing the others
   // from being displayed.
   TosKeyQuest_desc_rsc = \
      "Beelzebub wants me to kill some snakes, I think. "

   TosKeyQuest_kill_rsc = \
      "Beelzebub mentioned something. Go slay %i %s. "
      "Complete this task and report back to him."

   TosKeyQuest_return_rsc = \
      "Return to Beelzebub and let him know the task has been "
      "completed."

   TosKeyQuest_completed = \
      "You have completed Key to tos quest.  Return to Beelzebub. "

   Tos_Key = "help"

   // Quest resources.
   TosKeyQuest_assign_2 = \
      "Help?....yes.....snakes....go.....key...tos....."
   TosKeyQuest_failure_2 = \
      "..."
   TosKeyQuest_assign_3 = \
      "...good....yes....gold....hydras....go...."
   TosKeyQuest_failure_3 = \
      "..."
   TosKeyQuest_assign_4 = \
      "...good....yess....tyrants.....go....."
   TosKeyQuest_success_4 = \
      "...gooood...."
   TosKeyQuest_failure_4 = \
      "..."

classvars:

   vrIcon = TosKeyQuest_icon_rsc
   vrName = TosKeyQuest_name_rsc
   vrDesc = TosKeyQuest_desc_rsc

   vrKill = TosKeyQuest_kill_rsc
   vrReturn = TosKeyQuest_return_rsc
   vrCompleted = TosKeyQuest_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_TOS_KEY

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 30
   piSchedulePct = 100
   piTimeLimit = 10 * 3600 // 10 hours.

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_TOS_KEY_ONE, QNT_ID_TOS_KEY_TWO,
                      QNT_ID_TOS_KEY_THREE, QNT_ID_TOS_KEY_FOUR];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[Tos_Key],#quest_node_index=QNT_ID_TOS_KEY_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_TOS_KEY_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                                 #cNPC_class=&Beelzebub));
      }
      else
      {
         Debug("Error building QN: QNT_ID_TOS_KEY_ONE");
      }

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER,
               #NPC_modifier=QN_NPCMOD_SAME,#monsterlist=[&Snake],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_TOS_KEY_TWO,#amount_needed=5)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_TOS_KEY_TWO,
               #new_hint=TosKeyQuest_assign_2);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_TOS_KEY_TWO,
               #new_hint=TosKeyQuest_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_TOS_KEY_TWO");
      }

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER,
               #NPC_modifier=QN_NPCMOD_SAME,#monsterlist=[&GoldHydra],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_TOS_KEY_THREE,#amount_needed=5)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_TOS_KEY_THREE,
               #new_hint=TosKeyQuest_assign_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_TOS_KEY_THREE,
               #new_hint=TosKeyQuest_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_TOS_KEY_THREE");
      }

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER,
               #NPC_modifier=QN_NPCMOD_SAME,#monsterlist=[&Tyrant],
               #prizelist=[ [ [ QN_PRIZETYPE_ITEMCLASS, &TosKey ]] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_TOS_KEY_FOUR,#amount_needed=5)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_TOS_KEY_FOUR,
               #new_hint=TosKeyQuest_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_TOS_KEY_FOUR,
               #new_hint=TosKeyQuest_success_4);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_TOS_KEY_FOUR,
               #new_hint=TosKeyQuest_failure_4);
      }
      else
      {
         Debug("Error building QN: QNT_ID_TOS_KEY_FOUR");
      }

      return;
   }

   MainDesc(who=$,index=$,oQuestNode=$)
   {
      local oMob, oQE, iAmount;

      oQE = Send(SYS,@GetQuestEngine);

      oMob = Send(oQuestNode,@GetQuestMonster);

      iAmount = Send(oQE,@GetAmountToKill,#index=index);

      if oMob = $
      {
         AddPacket(4,vrReturn);
      }
      else
      {
         AddPacket(4,vrKill,4,iAmount,4,Send(oMob,@GetName));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
