// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
CastleVictoriaKeyQuest is QuestTemplate
// Quest nodes 239, 240, 241, 242
// QNT_ID_CASTLEVICTORIA_KEY_ONE, QNT_ID_CASTLEVICTORIA_KEY_TWO,
// QNT_ID_CASTLEVICTORIA_KEY_THREE, QNT_ID_CASTLEVICTORIA_KEY_FOUR

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   // Quest description.
   CastleVictoriaKeyQuest_icon_rsc = key3.bgf
   CastleVictoriaKeyQuest_name_rsc = "Key to Castle Victoria quest"

   // This description is used if there is an error preventing the others
   // from being displayed.
   CastleVictoriaKeyQuest_desc_rsc = \
      "Ceres of Qor wants you to hunt the wandering spirits. "

   CastleVictoriaKeyQuest_kill_rsc = \
      "Ceres of Qor wants you to slay %i %s. "
      "Complete this task and report back to him."

   CastleVictoriaKeyQuest_return_rsc = \
      "Return to Ceres of Qor and let him know the task has been "
      "completed."

   CastleVictoriaKeyQuest_completed = \
      "You have completed Key to Castle Victoria room quest.  Return to Ceres of Qor. "

   CastleVictoria_Key = "help"

   // Quest resources.
   CastleVictoriaKeyQuest_assign_2 = \
      "Help?..Some souls have gone free...spirits...put them back in there place..."
   CastleVictoriaKeyQuest_failure_2 = \
      "Damn spirits. Some better adventurer will handle them. "
   CastleVictoriaKeyQuest_assign_3 = \
      "MORE!...MORE!.."
   CastleVictoriaKeyQuest_failure_3 = \
      "Damn spirits. Some better adventurer will handle them. "
   CastleVictoriaKeyQuest_assign_4 = \
      "MORE!...MORE!..MORE!...MORE!..MORE!...MORE!.."
   CastleVictoriaKeyQuest_success_4 = \
      "Good harvest...the spirits are damned once more..."
   CastleVictoriaKeyQuest_failure_4 = \
      "Damn spirits. Some better adventurer will handle them. "

classvars:

   vrIcon = CastleVictoriaKeyQuest_icon_rsc
   vrName = CastleVictoriaKeyQuest_name_rsc
   vrDesc = CastleVictoriaKeyQuest_desc_rsc

   vrKill = CastleVictoriaKeyQuest_kill_rsc
   vrReturn = CastleVictoriaKeyQuest_return_rsc
   vrCompleted = CastleVictoriaKeyQuest_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_CASTLEVICTORIA_KEY

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 30
   piSchedulePct = 100
   piTimeLimit = 10 * 3600 // 10 hours.

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_CASTLEVICTORIA_KEY_ONE, QNT_ID_CASTLEVICTORIA_KEY_TWO,
                      QNT_ID_CASTLEVICTORIA_KEY_THREE, QNT_ID_CASTLEVICTORIA_KEY_FOUR];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[CastleVictoria_Key],#quest_node_index=QNT_ID_CASTLEVICTORIA_KEY_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_CASTLEVICTORIA_KEY_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                                 #cNPC_class=&CeresofQor));
      }
      else
      {
         Debug("Error building QN: QNT_ID_CASTLEVICTORIA_KEY_ONE");
      }

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER,
               #NPC_modifier=QN_NPCMOD_SAME,#monsterlist=[&Spirit],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_CASTLEVICTORIA_KEY_TWO,#amount_needed=5)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_CASTLEVICTORIA_KEY_TWO,
               #new_hint=CastleVictoriaKeyQuest_assign_2);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_CASTLEVICTORIA_KEY_TWO,
               #new_hint=CastleVictoriaKeyQuest_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_CASTLEVICTORIA_KEY_TWO");
      }

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER,
               #NPC_modifier=QN_NPCMOD_SAME,#monsterlist=[&Spirit],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_CASTLEVICTORIA_KEY_THREE,#amount_needed=10)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_CASTLEVICTORIA_KEY_THREE,
               #new_hint=CastleVictoriaKeyQuest_assign_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_CASTLEVICTORIA_KEY_THREE,
               #new_hint=CastleVictoriaKeyQuest_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_CASTLEVICTORIA_KEY_THREE");
      }

      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER,
               #NPC_modifier=QN_NPCMOD_SAME,#monsterlist=[&Spirit],
               #prizelist=[ [ [ QN_PRIZETYPE_ITEMCLASS, &CastleKey ]] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_CASTLEVICTORIA_KEY_FOUR,#amount_needed=15)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_CASTLEVICTORIA_KEY_FOUR,
               #new_hint=CastleVictoriaKeyQuest_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_CASTLEVICTORIA_KEY_FOUR,
               #new_hint=CastleVictoriaKeyQuest_success_4);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_CASTLEVICTORIA_KEY_FOUR,
               #new_hint=CastleVictoriaKeyQuest_failure_4);
      }
      else
      {
         Debug("Error building QN: QNT_ID_CASTLEVICTORIA_KEY_FOUR");
      }

      return;
   }

   MainDesc(who=$,index=$,oQuestNode=$)
   {
      local oMob, oQE, iAmount;

      oQE = Send(SYS,@GetQuestEngine);

      oMob = Send(oQuestNode,@GetQuestMonster);

      iAmount = Send(oQE,@GetAmountToKill,#index=index);

      if oMob = $
      {
         AddPacket(4,vrReturn);
      }
      else
      {
         AddPacket(4,vrKill,4,iAmount,4,Send(oMob,@GetName));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
