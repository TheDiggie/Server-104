// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DaemonHelm is Helmet

constants:

   include blakston.khd

resources:

   include dhelm.lkod

   dHelm_name_rsc = "daemon helm"
   dhelm_icon_male_rsc   = dhelma.bgf
   dhelm_icon_female_rsc = dhelmb.bgf
   dHelm_desc_rsc = \
      "The sight of this horned helmet strikes fear into your heart. "
      "It radiates an aura of power and strength, and looks well "
      "suited for combat."
  
   dHelm_use_rsc = \
      "Your blood boils with lust for battle while wearing the helmet. "
      "You yearn to strike down your enemies!"
  
   dHelm_unuse_rsc = \
      "The battle fury from the daemon helm leaves you. "
  
   dHelm_use_wav_rsc = bezerker_ring.ogg

classvars:

   vrName = dHelm_name_rsc
   vrDesc = dHelm_desc_rsc
   vrIcon = dhelm_icon_male_rsc

   viHits_init_min = 450
   viHits_init_max = 800

   viValue_average = 100
   viWeight = 100
   viBulk = 50

   viUse_type = ITEM_USE_HEAD
   viUse_amount = 1

   viGround_group = 3
   viInventory_group = 2
   viBroken_group = 4

   viDefense_base = 20
   viDamage_base = 1

properties:


messages:

   // Called by player, for normal 3rd-person overlays on user.
   GetOverlay(animation = $)
   {
      if Send(poOwner,@GetGender) = GENDER_FEMALE
      {
         return dhelm_icon_female_rsc;
      }

      return dhelm_icon_male_rsc;
   }

   NewUsed()
   {
      Send(poOwner,@RemoveHair,#what=self);

      Send(poOwner,@MsgSendUser,#message_rsc=dHelm_use_rsc);

      Send(poOwner,@AddAttackModifier,#what=self);
            
      propagate;
   }
     

   NewUnused()
   {
      Send(poOwner,@ResetHair,#what=self);
   
      Send(poOwner,@MsgSendUser,#message_rsc=dHelm_unuse_rsc);

      Send(poOwner,@RemoveAttackModifier,#what=self);

      propagate;
   } 
  
   ModifyDamage(who = $, what = $, damage = $)
   {
      local oWeapon;

      oWeapon = send(who,@GetWeapon);

      if oWeapon = $ OR NOT IsClass(oWeapon,&RangedWeapon)
      {
         return damage + random(2,4);
      }

      return damage;
   }

   WeaponHitTarget()
   {
      return;
   }
   
   ModifyHitRoll(who = $,what = $,hit_roll = $)
   {
      return hit_roll;
   }
   
   GetBaseSpellModifier(oSpell=$)
   {
      local iSchool;

      iSchool = Send(oSpell,@GetSchool);

      if (iSchool = SS_KRAANAN)
      {
         return 10;
      }

      if (iSchool = SS_QOR
         OR iSchool = SS_SHALILLE
         OR iSchool = SS_FAREN
         OR iSchool = SS_RIIJA
         OR iSchool = SS_JALA)
      {
         return -5;
      }

      return 0;
   }
   
   GetHeatDamage()
   {
      return 2;
   }

end
////////////////////////////////////////////////////////////////////////////////
